{
    package parser

    import (
        "github.com/brimdata/super/compiler/ast"
    )
}

start
  = __ scope:Scope EndQuery { return []any{scope}, nil }
  / __ seq:Seq EndQuery { return seq, nil }

EndQuery = __ ";"? __ EOF

Scope
  = decls:Decl+ body:Seq {
      return &ast.Scope{
        Kind: "Scope",
        Decls: sliceOf[ast.Decl](decls),
        Body: sliceOf[ast.Op](body),
        Loc: loc(c),
      }, nil
    }

Seq
  = first:LeanOp rest:SeqTail* {
      return prepend(first, rest), nil
    }

SeqTail = __ Pipe __ o:LeanOp { return o, nil }

Decl
  = v:(ConstDecl / FuncDecl / OpDecl / TypeDecl) _ { return v, nil }

ConstDecl
  = CONST _ name:Identifier __ "=" __ expr:Expr {
      return &ast.ConstDecl{
          Kind: "ConstDecl",
          Name: name.(*ast.ID),
          Expr: expr.(ast.Expr),
          Loc: loc(c),
      }, nil
    }

FuncDecl
  = FUNC _ name:Identifier __ "(" __ params:Identifiers? __ ")" __ ":" __ "(" __ expr:Expr __ ")" {
      return &ast.FuncDecl{
        Kind: "FuncDecl",
        Name: name.(*ast.ID),
        Params: sliceOf[*ast.ID](params),
        Expr: expr.(ast.Expr),
        Loc: loc(c),
      }, nil
    }

OpDecl
  = OP _ name:Identifier __ "(" __ params:Identifiers? __ ")" __ ":" __ "(" __ body:OpDeclBody __ ")" {
      return &ast.OpDecl{
        Kind: "OpDecl",
        Name: name.(*ast.ID),
        Params: sliceOf[*ast.ID](params),
        Body: sliceOf[ast.Op](body),
        Loc: loc(c),
      }, nil
    }

OpDeclBody
  = scope:Scope { return []any{scope}, nil }
  / seq:Seq { return seq, nil }

TypeDecl
  = TYPE _ name:Identifier __ "=" __ typ:Type {
      return &ast.TypeDecl{
        Kind: "TypeDecl",
        Name: name.(*ast.ID),
        Type: typ.(ast.Type),
        Loc: loc(c),
      }, nil
    }

// A LeanOp is a pipeline operator along with various forms of 
// "lean-forward" shortcuts: a scope with other LeanOps, a shortcut assignment 
// for "put", a shortcut "aggregate", or an expression that is either
// implied yield (non-boolean) or implied where (boolean).
LeanOp
  = Operator
  / "(" __ scope:Scope __ ")" { return scope, nil }
  / a:OpAssignment &EndOfOp { return a, nil }
  / !(Function EndOfOp) a:Aggregation &EndOfOp { return a, nil }
  / !((Identifier / Literal) __ (Pipe/EOF)) e:Expr {
      return &ast.OpExpr{Kind:"OpExpr",Expr:e.(ast.Expr),Loc:loc(c)}, nil
    }

EndOfOp = __ (Pipe / SearchKeywordGuard / "=>" / ")" / ";" / EOF)
Pipe = "|>" / "|" 

ExprGuard = __ ((!"=>" Comparator) / AdditiveOperator / MultiplicativeOperator / ":" / "(" / "[" / "~")

Comparator
  = ("==" / "=" / "!=" / "<>" / IN / LIKE / "<=" / "<" / ">=" / ">") { return string(c.text), nil }
  / NOT _ LIKE { return "not like", nil }

SearchBoolean
  = first:SearchAnd rest:SearchOrTerm* {
      return makeBinaryExprChain(first, rest, c), nil
    }

SearchOrTerm = _ OR _ t:SearchAnd { return []any{"or", t}, nil }

SearchAnd
  = first:SearchFactor
    rest:((_ AND)? _ !(OR / SearchKeywordGuard) expr:SearchFactor { return []any{"and", expr}, nil })* {
      return makeBinaryExprChain(first, rest, c), nil
    }

SearchKeywordGuard
  = FromSource __ "=>" __
  / Case __

SearchFactor
  = (NOT _ / "!" __) e:SearchFactor {
      return &ast.UnaryExpr{
          Kind: "UnaryExpr",
          Op: "!",
          Operand: e.(ast.Expr),
          Loc: loc(c),
      }, nil
    }
  / "(" __ expr:SearchBoolean __ ")" { return expr, nil }
  / SearchExpr

SearchExpr
  = Regexp
  / Glob
  / v:SearchValue (!ExprGuard / &(_ Glob)) {
      return &ast.Term{
        Kind: "Term",
        Text: string(c.text),
        Value: v.(ast.Any),
        Loc: loc(c),
      }, nil
    }
  / "*" !ExprGuard {
      return &ast.Primitive{Kind:"Primitive",Type:"bool",Text:"true",Loc:loc(c)}, nil
    }
  / SearchPredicate

SearchPredicate
  = lhs:AdditiveExpr __ op:Comparator __ rhs:AdditiveExpr {
      return &ast.BinaryExpr{
        Kind: "BinaryExpr",
        Op: op.(string),
        LHS: lhs.(ast.Expr),
        RHS: rhs.(ast.Expr),
        Loc: loc(c),
      }, nil
    }
  / f:Function { return f, nil }

SearchValue
  = Literal
  / !RegexpPattern v:KeyWord {
      return newPrimitive(c, "string", v.(string)), nil
    }

Glob
  = pattern:GlobPattern {
      return &ast.Glob{Kind: "Glob", Pattern: pattern.(string), Loc: loc(c)}, nil
    }

Regexp
  = pattern:RegexpPattern {
      return &ast.Regexp{Kind:"Regexp", Pattern:pattern.(string), Loc: loc(c)}, nil
    }

/// === Aggregations ===

Aggregation
  = Aggregate? keys:AggregateKeys limit:LimitArg {
      return &ast.Aggregate{
        Kind: "Aggregate",
        Keys: sliceOf[ast.Assignment](keys),
        Limit: limit.(int),
        Loc: loc(c),
      }, nil
    }
  / Aggregate? aggs:AggAssignments keys:(_ AggregateKeys)? limit:LimitArg {
      s := &ast.Aggregate {
        Kind: "Aggregate",
        Aggs: sliceOf[ast.Assignment](aggs),
        Limit: limit.(int),
        Loc: loc(c),
      }
      if keys != nil {
        s.Keys = sliceOf[ast.Assignment](keys.([]any)[1])
      }
      return s, nil
    }

Aggregate = (AGGREGATE / SUMMARIZE) _

AggregateKeys
  = (GROUP _)? BY _ columns:FlexAssignments { return columns, nil }

LimitArg
  = _ WITH _ "-limit" _ limit:UInt { return limit, nil }
  / "" { return 0, nil }

// A FlexAssignment is like an Assignment but it can optionally omit the lhs,
// in which case the semantic pass will infer a name from the rhs, e.g., for
// an expression like "count() by foo", the rhs is Field "foo" and the lhs is nil.
FlexAssignment
  = Assignment
  / expr:Expr { return ast.Assignment{Kind:"Assignment",RHS:expr.(ast.Expr),Loc:loc(c)}, nil }

FlexAssignments
  = first:FlexAssignment rest:(__ "," __ expr:FlexAssignment { return expr, nil })* {
      return prepend(first, rest), nil
    }

AggAssignment
  = lval:Lval __ ":=" __ agg:Agg {
      return ast.Assignment{Kind:"Assignment",LHS:lval.(ast.Expr),RHS:agg.(ast.Expr),Loc:loc(c)}, nil
    }
  / agg:Agg {
      return ast.Assignment{Kind:"Assignment",RHS:agg.(ast.Expr),Loc:loc(c)}, nil
    }

Agg
  = !FuncGuard aggDistinct:AggDistinct !(__ ".") where:WhereClause? {
      agg := aggDistinct.(*ast.Agg)
      agg.Loc = loc(c)
      if where != nil {
        agg.Where = where.(ast.Expr)
      }
      return agg, nil
    }
  / !FuncGuard name:AggName __ "(" __ expr:(OverExpr / Expr)? __ ")" !(__ ".") where:WhereClause? {
      agg := &ast.Agg{
          Kind: "Agg",
          Name: name.(string),
          Loc: loc(c),
      }
      if expr != nil {
        agg.Expr = expr.(ast.Expr)
      }
      if where != nil {
        agg.Where = where.(ast.Expr)
      }
      return agg, nil
    }
  / cs:CountStar {
      return &ast.Agg{
          Kind: "Agg",
          Name: "count",
          Loc: cs.(*ast.Call).Loc,
      }, nil
    }

AggDistinct
  = !FuncGuard name:AggName __ "(" __ DISTINCT _ expr:(OverExpr / Expr) __ ")" {
      return &ast.Agg{
          Kind: "Agg",
          Name: name.(string),
          Distinct: true,
          Expr: expr.(ast.Expr),
          Loc: loc(c),
      }, nil
    }

AggName
  = IdentifierName
  / AND
  / OR

WhereClause = _ WHERE _ expr:LogicalOrExpr { return expr, nil }

AggAssignments
  = first:AggAssignment rest:(__ "," __ AggAssignment)* {
      result := []any{first}
      for _, r := range rest.([]any) {
          result = append(result, r.([]any)[3])
      }
      return result, nil
    }

CountStar = COUNT __ "(" __ "*" __ ")" {
    return &ast.Call{
		Kind: "Call",
		Name: &ast.ID{Kind:"ID", Name:"count", Loc: ast.NewLoc(c.pos.offset, c.pos.offset+5)},
        Loc:  loc(c),
    }, nil
  }

/// === Operators ===

Operator
  = op:SelectOp &EndOfOp { return op, nil }
  / ForkOp
  / SwitchOp 
  / FromForkOp
  / SearchOp
  / AssertOp
  / SortOp
  / TopOp
  / CutOp
  / DistinctOp
  / DropOp
  / HeadOp
  / TailOp
  / SkipOp
  / WhereOp
  / UniqOp
  / PutOp
  / RenameOp
  / FuseOp
  / ShapeOp
  / JoinOp
  / SampleOp
  / FromOp
  / PassOp
  / ExplodeOp
  / MergeOp
  / OverOp
  / YieldOp
  / LoadOp
  / OutputOp
  / DebugOp

PipeKeyword
  = SELECT / FORK / SWITCH / FROM / SEARCH / ASSERT / SORT / TOP/ CUT / DROP
  / HEAD / TAIL / WHERE / UNIQ / PUT / RENAME / FUSE / SHAPE / JOIN / SAMPLE
  / FROM / PASS / EXPLODE / MERGE / OVER / YIELD / LOAD / OUTPUT / DEBUG
  / AGGREGATE / SUMMARIZE

 ForkOp =
   FORK __ "(" paths:Path+ __ ")" {
      var seqs []ast.Seq
      for _, p := range paths.([]any) {
        seqs = append(seqs, sliceOf[ast.Op](p))
      }
      return &ast.Parallel{
          Kind: "Parallel",
          Paths: seqs,
          Loc: loc(c),
      }, nil
    }

Path = __ "=>" __ seq:Seq { return seq, nil }

SwitchOp
  = SWITCH _ expr:Expr _ "(" cases:SwitchPath+ __ ")" {
      return &ast.Switch{
          Kind: "Switch",
          Expr: expr.(ast.Expr),
          Cases: sliceOf[ast.Case](cases),
          Loc: loc(c),
      }, nil
    }
  / SWITCH __ "(" cases:SwitchPath+ __ ")" {
      return &ast.Switch{
          Kind: "Switch",
          Cases: sliceOf[ast.Case](cases),
          Loc: loc(c),
      }, nil
    }

SwitchPath
  = __ expr:Case path:Path {
      l := ast.Case{Path: sliceOf[ast.Op](path)}
      if expr != nil {
        l.Expr = expr.(ast.Expr)
      }
      return l, nil
    }

Case
  = CASE _ expr:Expr { return expr, nil }
  / DEFAULT { return nil, nil }

FromForkOp
  = FROM __ "(" trunks:FromPath+ __ ")" {
      return &ast.Parallel{
          Kind: "Parallel",
          Paths: sliceOf[ast.Seq](trunks),
          Loc: loc(c),
      }, nil
    }

FromPath
  = __ source:FromSource seq:(__ "=>" __ s:Seq { return s, nil })? {
      ops := []any{source}
      if seq != nil {
        ops = append(ops, seq.([]any)...)
      }
      return ast.Seq(sliceOf[ast.Op](ops)), nil
    }

FromSource
  = FromKeyWord _ elem:FromElem {
        return &ast.From{
            Kind: "From",
            Elems: []*ast.FromElem{elem.(*ast.FromElem)},
            Loc: loc(c),
        }, nil
    }
  / PassOp

SearchOp
  = (SEARCH _ / "?" __) expr:SearchBoolean {
      return &ast.Search{Kind:"Search",Expr:expr.(ast.Expr),Loc:loc(c)}, nil
    }

AssertOp
  = ASSERT _ expr:(e:Expr { return []any{e, string(c.text)}, nil }) {
      return &ast.Assert{
          Kind: "Assert",
          Expr: expr.([]any)[0].(ast.Expr),
          Text: expr.([]any)[1].(string),
          Loc: loc(c),
      }, nil
    }

SortOp
  = (SORT / ORDER _ BY) &EOKW args:SortArgs exprs:(__ e:OrderByList { return e, nil })? {
      o := &ast.Sort{
        Kind: "Sort",
        Loc: loc(c),
      }
      if exprs != nil {
        o.Exprs = exprs.([]ast.SortExpr)
      }
      argm := args.(map[string]any)
      if _, ok := argm["r"]; ok {
        o.Reverse = true
      }
      return o, nil
    }

SortArgs = args:(_ a:SortArg { return a, nil })* { return makeArgMap(args) }

SortArg
  = "-r" { return map[string]any{"name": "r", "value": nil}, nil }

TopOp
  = TOP &EOKW args:SortArgs limit:(_ e:Expr { return e, nil })? exprs:(_ e:OrderByList { return e, nil })? {
      op := &ast.Top{
        Kind: "Top",
        Loc: loc(c),
      }
      if limit != nil {
        op.Limit = limit.(ast.Expr)
      }
      if exprs != nil {
        op.Exprs = exprs.([]ast.SortExpr)
      }
      argm := args.(map[string]any)
      if _, ok := argm["r"]; ok {
        op.Reverse = true
      }
      return op, nil
    }

CutOp
  = CUT _ args:FlexAssignments {
      return &ast.Cut{
        Kind: "Cut",
        Args: sliceOf[ast.Assignment](args),
        Loc: loc(c),
      }, nil
    }

DistinctOp
  = DISTINCT _ e:Expr {
      return &ast.Distinct{
          Kind: "Distinct",
          Expr: e.(ast.Expr),
          Loc: loc(c),
      }, nil
    }

DropOp
  = DROP _ args:Lvals {
      return &ast.Drop{
          Kind: "Drop",
          Args: sliceOf[ast.Expr](args),
          Loc: loc(c),
      }, nil
    }

HeadOp
  = (HEAD / LIMIT) _ !EndOfOp count:Expr {
      return &ast.Head{
        Kind: "Head",
        Count: count.(ast.Expr),
        Loc: loc(c),
      }, nil
    }
  / HEAD !(__ "(") &EOKW {
      return &ast.Head{
        Kind: "Head",
        Loc: loc(c),
      }, nil
    }

TailOp
  = TAIL _ !EndOfOp count:Expr {
      return &ast.Tail{
        Kind: "Tail",
        Count: count.(ast.Expr),
        Loc: loc(c),
      }, nil
    }
  / TAIL !(__ "(") &EOKW {
      return &ast.Tail{
        Kind: "Tail",
        Loc: loc(c),
      }, nil
    }

SkipOp
  = SKIP _ count:Expr {
      return &ast.Skip{
        Kind: "Skip",
        Count: count.(ast.Expr),
        Loc: loc(c),
      }, nil
    }

WhereOp
  = WHERE _ expr:Expr {
      return &ast.Where{
        Kind: "Where",
        Expr: expr.(ast.Expr),
        Loc: loc(c),
      }, nil
    }

UniqOp
  = UNIQ _ "-c" {
      return &ast.Uniq{Kind: "Uniq", Cflag: true, Loc: loc(c)}, nil
    }
  / UNIQ !(__ "(") &EOKW {
      return &ast.Uniq{Kind: "Uniq", Loc: loc(c)}, nil
    }

PutOp
  = PUT _ args:Assignments {
      return &ast.Put{
          Kind: "Put",
          Args: sliceOf[ast.Assignment](args),
          Loc: loc(c),
      }, nil
    }

RenameOp
  = RENAME _ first:Assignment rest:(__ "," __ cl:Assignment { return cl, nil })* {
      args := append([]any{first}, rest.([]any)...)
      return &ast.Rename{
        Kind: "Rename",
        Args: sliceOf[ast.Assignment](args),
        Loc: loc(c),
      }, nil
    }

// The paren guard is to allow parsing fuse() as an aggregator. This
// is temporary pending finalizing the names
// (https://github.com/brimdata/super/issues/2114) for the fuse op and
// aggregator, at which point we may no longer need this.
FuseOp
  = FUSE !(__ "(") &EOKW {
      return &ast.Fuse{Kind: "Fuse", Loc: loc(c)}, nil
    }

ShapeOp
  = SHAPE !(__ "(") &EOKW {
      return &ast.Shape{Kind: "Shape", Loc: loc(c)}, nil
    }

JoinOp
  = style:JoinStyle JOIN rightInput:JoinRightInput e:JoinExpr optArgs:(_ FlexAssignments)? {
      o := &ast.Join{
        Kind: "Join",
        Style: style.(string),
        Cond: e.(ast.JoinExpr),
        Loc: loc(c),
      }
      if rightInput != nil {
        o.RightInput = sliceOf[ast.Op](rightInput)
      }
      if optArgs != nil {
        o.Args = sliceOf[ast.Assignment](optArgs.([]any)[1])
      }
      return o, nil
    }

JoinStyle
  = ANTI _  { return "anti", nil }
  / INNER _ { return "inner", nil }
  / LEFT  _ { return "left", nil }
  / RIGHT _ { return "right", nil }
  / ""      { return "", nil }

JoinRightInput
  = __ "(" __ s:Seq __ ")" { return s, nil }
  / "" { return nil, nil }

JoinKey
  = Lval
  / "(" expr:Expr ")" { return expr, nil }

SampleOp
  = SAMPLE &EOKW expr:(_ e:Lval { return e, nil })? {
      o := &ast.Sample{Kind: "Sample", Loc: loc(c)}
      if expr != nil {
        o.Expr = expr.(ast.Expr)
      }
      return o, nil
    }

// OpAssignment is a top level operator that contains a list of assignment
// expressions. It is in an ambiguous state and could either be:
//   1) An aggregate operator.
//   2) A put operator.
//  The semantic pass is responsible for converting it to one of the two.
OpAssignment
  = a:Assignments {
      return &ast.OpAssignment{
          Kind: "OpAssignment",
          Assignments: sliceOf[ast.Assignment](a),
          Loc: loc(c),
      }, nil
    }

LoadOp
  = LOAD _ pool:Name branch:PoolBranch? author:AuthorArg? message:MessageArg? meta:MetaArg? {
      return &ast.Load{
        Kind: "Load",
        Pool: nullableName(pool),
        Branch: nullableName(branch),
        Author: nullableName(author),
        Message: nullableName(message),
        Meta: nullableName(meta),
        Loc: loc(c),
      }, nil
    }

AuthorArg
  = _ AUTHOR _ n:Name { return n, nil }

MessageArg
  = _ MESSAGE _ n:Name { return n, nil }

MetaArg
  = _ META _ n:Name { return n, nil }

PoolBranch
  = "@" branch:Name { return branch, nil }

OutputOp
  = OUTPUT _ name:Identifier {
      return &ast.Output{
        Kind: "Output",
        Name: name.(*ast.ID),
        Loc: loc(c),
      }, nil
    }

DebugOp
  = DEBUG &EOKW expr:(_ e:Expr { return e, nil })? {
      d := &ast.Debug{
        Kind: "Debug",
        Loc: loc(c),
      }
      if expr != nil {
        d.Expr = expr.(ast.Expr)
      }
      return d, nil
    }

FromOp
  = FromKeyWord _ elems:FromElems {
      return &ast.From{
        Kind: "From",
        Elems: sliceOf[*ast.FromElem](elems),
        Loc: loc(c),
      }, nil
  }

FromKeyWord
  = FROM
  / DeprecatedFroms

DeprecatedFroms = "get" / "file" / "pool"

FromElems
  = first:FromElem rest:( __ "," __ elem:FromElem { return elem, nil} )* {
      return prepend(first, rest), nil
    }
    
FromElem
  = entity:FromEntity args:FromArgs o:OptOrdinality alias:OptAlias {
      elem := &ast.FromElem{
        Kind: "FromElem",
        Entity: entity.(ast.FromEntity),
        Loc: loc(c),
      }
      if args != nil {
        elem.Args = args.(ast.FromArgs)
      }
      if o != nil {
        elem.Ordinality = o.(*ast.Ordinality)
      }      
      if alias != nil {
        elem.Alias = alias.(*ast.TableAlias)
      }
      return elem, nil
    }

FromEntity
  = url:UnquotedURL {
      return &ast.Name{
        Kind: "Name",
        Text: url.(string),
        Loc: loc(c),
      }, nil
    }
  / Regexp
  / Glob
  / "*" !ExprGuard { return &ast.Glob{Kind: "Glob", Pattern: "*", Loc: loc(c)}, nil }
  / EVAL __ "(" __ e:Expr __ ")" {
      return &ast.ExprEntity{
        Kind: "ExprEntity",
        Expr: e.(ast.Expr),
        Loc: loc(c),
      }, nil
    }
  / meta:PoolMeta {
      return &ast.LakeMeta{
        Kind: "LakeMeta",
        Meta: nullableName(meta),
        Loc: loc(c),
      }, nil
    }
  / join:JoinOperation !AliasClause {return join, nil}
  / "(" __ join:JoinOperation __ ")" { return join, nil }
  / "(" __ s:SQLPipe __ ")" {
      return s, nil
    }
   / name:Name    { return name, nil }
  // XXX UNNEST, PIVOT, UNPIVOT, type projection, left correlation?

FromArgs
  = commit:PoolCommit meta:PoolMeta? tap:TapArg {
      return &ast.PoolArgs{
        Kind: "PoolArgs",
        Commit: nullableName(commit),
        Meta: nullableName(meta),
        Tap: tap.(bool),
        Loc: loc(c),
      }, nil
    }
  / meta:PoolMeta tap:TapArg {
      return &ast.PoolArgs{
        Kind: "PoolArgs",
        Meta: nullableName(meta),
        Tap: tap.(bool),
        Loc: loc(c),
      }, nil
    }
  / format:FormatArg !(_ (METHOD / HEADERS / BODY)) {
      return &ast.FormatArg{
        Kind: "FormatArg",
        Format: nullableName(format),
        Loc: loc(c),
      }, nil
  }
  / format:FormatArg? method:MethodArg? headers:HeadersArg? body:BodyArg? {
      if format == nil && method == nil && headers == nil && body == nil {
        return nil, nil
      }
      h := &ast.HTTPArgs{
        Kind: "HTTPArgs",
        Format: nullableName(format),
        Method: nullableName(method),
        Body: nullableName(body),
        Loc: loc(c),
      }
      if headers != nil {
        h.Headers = headers.(*ast.RecordExpr)
      }
      return h, nil
    }

FormatArg = _ FORMAT _ n:Name { return n, nil }

MethodArg = _ METHOD _ n:Name { return n, nil }

HeadersArg = _ HEADERS _ v:Record { return v, nil }

BodyArg = _ BODY _ n:Name { return n, nil }

UnquotedURL = ("http://" / "https://") URLChar+ { return string(c.text), nil }

URLChar = [0-9a-zA-Z!@$%&_=,./?:[\]~+-]

//XXX this should be a timestamp
PoolAt
  = _ AT _ id:KSUID { return id, nil }

//XXX this should allow 0x bytes format
KSUID = ([0-9a-zA-Z])+ { return string(c.text), nil }

PoolCommit
  = "@" n:Name { return n, nil }

PoolMeta
  = ":" n:Name { return n, nil }

TapArg
  = _ TAP { return true, nil }
  / "" { return false, nil }

PassOp
  = PASS !(__ "(") &EOKW {
      return &ast.Pass{Kind: "Pass", Loc: loc(c)}, nil
    }

// Currently we support only "explode by type" but this will be generalized
// to explode arbitrary arrays, records, etc.
ExplodeOp
  = EXPLODE _ args:Exprs typ:TypeArg as:AsArg? {
      o := &ast.Explode{
        Kind: "Explode",
        Args: sliceOf[ast.Expr](args),
        Type: typ.(ast.Type),
        Loc: loc(c),
      }
      if as != nil {
        o.As = as.(ast.Expr)
      }
      return o, nil
    }

MergeOp
  = MERGE _ exprs:OrderByList {
      return &ast.Merge{
        Kind: "Merge",
        Exprs: exprs.([]ast.SortExpr),
        Loc: loc(c),
      }, nil
    }

OverOp
  = (OVER / UNNEST) _ exprs:Exprs locals:Locals? body:Lateral? {
      o := &ast.Over{
        Kind: "Over",
        Exprs: sliceOf[ast.Expr](exprs),
        Locals: sliceOf[ast.Def](locals),
        Loc: loc(c),
      }
      if body != nil {
        o.Body = sliceOf[ast.Op](body.([]any)[0])
      }
      return o, nil
    }

//XXX these locators in the any slice need to go

Lateral
  = __ "=>" __ "(" __ scope:Scope __ ")" {
      return []any{[]any{scope.(*ast.Scope)}, loc(c).End()}, nil
    }
  / __ "=>" __ "(" __ seq:Seq __ ")" {
      return []any{seq, loc(c).End()}, nil
    }

Locals
  = _ WITH _ first:LocalsAssignment rest:(__ "," __ a:LocalsAssignment { return a, nil })* {
      return prepend(first, rest), nil
    }

LocalsAssignment
  = name:Identifier opt:(__ "=" __ Expr)? {
      def := ast.Def{
        Name: name.(*ast.ID),
        Expr: name.(*ast.ID),
        Loc: loc(c),
      }
      if opt != nil {
        def.Expr = opt.([]any)[3].(ast.Expr)
      }
      return def, nil
    }

YieldOp
  = YIELD _ exprs:Exprs {
      return &ast.Yield{
          Kind: "Yield",
          Exprs: sliceOf[ast.Expr](exprs),
          Loc: loc(c),
      }, nil
    }

TypeArg
  = _ BY _ typ:Type { return typ, nil }

AsArg
  = _ AS _ lhs:Lval { return lhs, nil }

/// === Expressions ===

Lval = DerefExpr

Lvals
  = first:Lval rest:(__ "," __ lval:Lval { return lval, nil })* {
      return prepend(first, rest), nil
    }

Assignments
  = first:Assignment rest:(__ "," __ a:Assignment { return a, nil })* {
    return prepend(first, rest), nil
  }

Assignment
  = lhs:Lval __ ":=" __ rhs:Expr {
      return ast.Assignment{
          Kind: "Assignment",
          LHS: lhs.(ast.Expr),
          RHS: rhs.(ast.Expr),
          Loc: loc(c),
      }, nil
    }

Expr = ConditionalExpr

ConditionalExpr
  = cond:LogicalOrExpr opt:(__ "?" __ Expr __ ":" __ Expr)? {
      if opt != nil {
        t, e := opt.([]any)[3], opt.([]any)[7]
        return &ast.Conditional{
            Kind: "Conditional",
            Cond: cond.(ast.Expr),
            Then: t.(ast.Expr),
            Else: e.(ast.Expr),
            Loc: loc(c),
        }, nil
      }
      return cond, nil
    }

LogicalOrExpr
  = first:LogicalAndExpr
    rest:(__ op:OR __ expr:LogicalAndExpr { return []any{op, expr}, nil })* {
        return makeBinaryExprChain(first, rest, c), nil
    }

LogicalAndExpr
  = first:NotExpr
    rest:(__ op:AND __ expr:NotExpr { return []any{op, expr}, nil })* {
        return makeBinaryExprChain(first, rest, c), nil
    }

NotExpr
  = (NOT _ / "!" __) e:NotExpr {
      return &ast.UnaryExpr{
          Kind: "UnaryExpr",
          Op: "!",
          Operand: e.(ast.Expr),
          Loc: loc(c),
      }, nil
    }
  / BetweenExpr
  
BetweenExpr
  = expr:ComparisonExpr _ not:(NOT _)? BETWEEN _ lower:BetweenExpr _ AND _ upper:BetweenExpr {
      return &ast.Between{
        Kind: "Between",
        Not: not != nil,
        Expr: expr.(ast.Expr),
        Lower: lower.(ast.Expr),
        Upper: upper.(ast.Expr),
        Loc: loc(c),
      }, nil
    }
  / ComparisonExpr

ComparisonExpr
  = expr:AdditiveExpr _ IS not:(_ NOT)? _ NULL {
      return &ast.IsNullExpr{
        Kind: "IsNullExpr",
        Expr: expr.(ast.Expr),
        Not: not != nil,
        Loc: loc(c),
      }, nil
    }   
  / lhs:AdditiveExpr opAndRHS:(__ Comparator __ AdditiveExpr / __ ("~" { return string(c.text), nil }) __ Regexp)? {
      if opAndRHS == nil {
        return lhs, nil
      }
      return &ast.BinaryExpr{
        Kind: "BinaryExpr",
        Op: opAndRHS.([]any)[1].(string),
        LHS: lhs.(ast.Expr),
        RHS: opAndRHS.([]any)[3].(ast.Expr),
        Loc: loc(c),
      }, nil
    }
 
AdditiveExpr
  = first:MultiplicativeExpr
    rest:(__ op:AdditiveOperator __ expr:MultiplicativeExpr { return []any{op, expr}, nil })* {
        return makeBinaryExprChain(first, rest, c), nil
    }

AdditiveOperator = ("+" / "-") { return string(c.text), nil }

MultiplicativeExpr
  = first:ConcatExpr
    rest:(__ op:MultiplicativeOperator __ expr:ConcatExpr { return []any{op, expr}, nil })* {
        return makeBinaryExprChain(first, rest, c), nil
    }

MultiplicativeOperator = ("*" / "/" / "%") { return string(c.text), nil }

ConcatExpr 
  = first:UnaryPlusOrMinus
    rest:(__ "||" __ expr:UnaryPlusOrMinus { return []any{"||", expr}, nil })* {
        return makeBinaryExprChain(first, rest, c), nil
    }

UnaryPlusOrMinus
  = !Literal op:PlusOrMinusOp __ e:UnaryPlusOrMinus
 {
      return &ast.UnaryExpr{
          Kind: "UnaryExpr",
          Op: op.(string),
          Operand: e.(ast.Expr),
          Loc: loc(c),
      }, nil
    }
  / DerefExpr

PlusOrMinusOp = ("+" / "-") { return string(c.text), nil }

DerefExpr
  = expr:DerefExpr "[" __ from:AdditiveExpr __ ":" __ to:AdditiveExpr? __ "]" {
      e := &ast.SliceExpr{
        Kind: "SliceExpr",
        Expr: expr.(ast.Expr),
        From: from.(ast.Expr),
        Loc: loc(c),
      }
      if to != nil {
        e.To = to.(ast.Expr)
      }
      return e, nil
    }
  / expr:DerefExpr "[" __ ":" __ to:AdditiveExpr "]" {
      return &ast.SliceExpr{
        Kind: "SliceExpr",
        Expr: expr.(ast.Expr),
        To: to.(ast.Expr),
        Loc: loc(c),
      }, nil
    }
  / expr:DerefExpr "[" index:Expr "]" {
      return &ast.IndexExpr{
        Kind: "IndexExpr",
        Expr: expr.(ast.Expr),
        Index: index.(ast.Expr),
        Loc: loc(c),
      }, nil
    }
  / expr:DerefExpr "." id:DerefKey {
      return &ast.BinaryExpr{
        Kind: "BinaryExpr",
        Op: ".",
        LHS: expr.(ast.Expr),
        RHS: id.(ast.Expr),
        Loc: loc(c),
      }, nil
    }
  / FuncExpr
  / Primary

DerefKey
  = Identifier 
  / s:DoubleQuotedString    { return &ast.ID{Kind: "ID", Name:s.(string), Loc: loc(c)}, nil }
  / s:BacktickString        { return &ast.ID{Kind: "ID", Name:s.(string), Loc: loc(c)}, nil }

FuncExpr
  = Cast
  / Function

FuncGuard = NotFuncs __ "("

NotFuncs
  = NOT
  / SELECT

Cast
  = typ:TypeLiteral __ "(" __ expr:(OverExpr / Expr) __ ")" {
      return &ast.Cast{Kind: "Cast", Expr: expr.(ast.Expr), Type: typ.(ast.Expr), Loc: loc(c)}, nil
    }

Function
  = Grep
  // Special case to handle `regexp(/x/, y)` and `regexp_replace(x, /y/, z)`.
  / REGEXP __ "(" __ arg0:RegexpPrimitive __ "," __ arg1:Expr __ ")" where:WhereClause? {
      id := &ast.ID{Kind: "ID", Name: "regexp", Loc: ast.NewLoc(c.pos.offset, c.pos.offset+6)}
      return newCall(c, id, []any{arg0, arg1}, where), nil
    }
  / REGEXP_REPLACE __ "(" __ arg0:Expr __ "," __ arg1:RegexpPrimitive __ "," __ arg2:Expr __ ")" where:WhereClause? {
      id := &ast.ID{Kind: "ID", Name: "regexp_replace", Loc: ast.NewLoc(c.pos.offset, c.pos.offset+14)}
      return newCall(c, id, []any{arg0, arg1, arg2}, where), nil
    }
  / !FuncGuard EXTRACT __ "(" __ part:Expr _ FROM _ e:Expr __ ")" where:WhereClause? {
      return &ast.CallExtract{
        Kind: "CallExtract",
        Part: part.(ast.Expr),
        Expr: e.(ast.Expr), 
        Loc: loc(c),
      }, nil
    }
  / !FuncGuard CAST __ "(" __ e:Expr _ AS _ typ:Identifier __ ")" {
      return &ast.SQLCast{
        Kind: "SQLCast",
        Expr: e.(ast.Expr),
        Type: typ.(*ast.ID),
        Loc: loc(c),
      }, nil
    }
  / !FuncGuard SUBSTRING __ "(" __ expr:Expr from:(_ FROM _ e:Expr { return e, nil })? for_:(_ FOR _ e:Expr { return e, nil })? ")" {
      s := &ast.SQLSubstring{
        Kind: "SQLSubstring",
        Expr: expr.(ast.Expr),
        Loc: loc(c),
      }
      if from != nil {
        s.From = from.(ast.Expr)
      }
      if for_ != nil {
        s.For = for_.(ast.Expr)
      }
      return s, nil
    }
  / !FuncGuard fn:Identifier __ "(" __ args:FunctionArgs __ ")" where:WhereClause? {
      return newCall(c, fn, args, where), nil
    }
  / CountStar

RegexpPrimitive
  = pat:RegexpPattern { return newPrimitive(c, "string", pat.(string)), nil }

FunctionArgs
  = o:OverExpr { return []any{o}, nil }
  / OptionalExprs

Grep
  = GREP __ "(" __ pattern:(Regexp / Glob / Expr) __ opt:("," __ e:(OverExpr / Expr) __ { return e, nil })? ")" {
      g := &ast.Grep{
          Kind: "Grep",
          Pattern: pattern.(ast.Expr),
          Loc: loc(c),
      }
      if opt != nil {
          g.Expr = opt.(ast.Expr)
      }
      return g, nil
    }

OptionalExprs
  = Exprs
  / __ { return []any{}, nil }

Exprs
  = first:Expr rest:(__ "," __ e:Expr { return e, nil })* {
      return prepend(first, rest), nil
    }

Primary
  = CaseExpr
  / Record
  / Array
  / Set
  / Map
  / SQLTimeValue
  / Literal
  / !PipeKeyword id:Identifier { return id, nil }
  / Tuple
  / "(" __ expr:OverExpr __ ")" { return expr, nil }
  / "(" __ expr:Expr __ ")" { return expr, nil }

CaseExpr
  = CASE cases:When+ else_:(_ ELSE _ Expr)? _ END (_ CASE)? {
        whens := sliceOf[ast.When](cases)
        cond := &ast.Conditional{
            Kind: "Conditional",
            Cond: whens[0].Cond,
            Then: whens[0].Then,
            Loc: loc(c),
        }
        last := cond
        for _, when := range whens[1:] {
            next := &ast.Conditional{
               Kind: "Conditional",
               Cond: when.Cond,
               Then: when.Then,
               Loc: when.Loc,
            }
            last.Else = next
            last = next
        }
        if else_ != nil {
            last.Else = else_.([]any)[3].(ast.Expr)
        }
        return cond, nil
    }
  / CASE _ expr:Expr whens:When+ else_:(_ ELSE _ Expr)? _ END (_ CASE)?  {
        cond := &ast.CaseExpr{
            Kind: "CaseExpr",
            Expr: expr.(ast.Expr),
            Whens: sliceOf[ast.When](whens),
            Loc: loc(c),
        }
        if else_ != nil {
            cond.Else = else_.([]any)[3].(ast.Expr)
        }
        return cond, nil
    }

When
  = _ WHEN _ cond:Expr _ THEN _ then:Expr {
      return ast.When{
        Kind: "When",
        Cond: cond.(ast.Expr),
        Then: then.(ast.Expr),
        Loc: loc(c),
      }, nil
    }
    
OverExpr
  = OVER _ exprs:Exprs locals:Locals? __ Pipe __ body:Seq {
      return &ast.OverExpr{
          Kind: "OverExpr",
          Locals: sliceOf[ast.Def](locals),
          Exprs: sliceOf[ast.Expr](exprs),
          Body: sliceOf[ast.Op](body),
          Loc: loc(c),
      }, nil
    }

Record
  = "{" __ elems:RecordElems __ "}" {
      return &ast.RecordExpr{
          Kind: "RecordExpr",
          Elems: sliceOf[ast.RecordElem](elems),
          Loc: loc(c),
      }, nil
    }

RecordElems
  = first:RecordElem rest:RecordElemTail* {
    return prepend(first, rest), nil
  }
  / __ { return nil, nil }

RecordElemTail = __ "," __ elem:RecordElem { return elem, nil }

RecordElem
  = Spread
  / FieldExpr
  / Identifier

Spread
  = "..." __ expr:Expr {
      return &ast.Spread{Kind: "Spread", Expr: expr.(ast.Expr), Loc: loc(c)}, nil
    }

FieldExpr
  = name:Name __ ":" __ value:Expr {
      return &ast.FieldExpr{
        Kind: "FieldExpr",
        Name: name.(*ast.Name),
        Value: value.(ast.Expr),
        Loc: loc(c),
      }, nil
    }

Array
  = "[" __ elems:VectorElems __ "]" {
      return &ast.ArrayExpr{
          Kind: "ArrayExpr",
          Elems: sliceOf[ast.VectorElem](elems),
          Loc: loc(c),
      }, nil
    }

Set
  = "|[" __ elems:VectorElems __ "]|" {
      return &ast.SetExpr{
          Kind: "SetExpr",
          Elems: sliceOf[ast.VectorElem](elems),
          Loc: loc(c),
      }, nil
    }

VectorElems
  = first:VectorElem rest:(__ "," __ e:VectorElem { return e, nil })* {
      return prepend(first, rest), nil
    }
  / __ { return nil, nil }

VectorElem
  = Spread
  / e:Expr { return &ast.VectorValue{Kind: "VectorValue", Expr: e.(ast.Expr),Loc:loc(c)}, nil }

Map
  = "|{" __ exprs:Entries __ "}|" {
      return &ast.MapExpr{
          Kind: "MapExpr",
          Entries: sliceOf[ast.EntryExpr](exprs),
          Loc: loc(c),
      }, nil
    }

Entries
  = first:Entry rest:EntryTail* {
    return prepend(first, rest), nil
  }
  / __ { return nil, nil }


EntryTail = __ "," __ e:Entry { return e, nil }

Entry
  = key:Expr __ ":" __ value:Expr {
      return ast.EntryExpr{Key: key.(ast.Expr), Value: value.(ast.Expr),Loc:loc(c)}, nil
    }

Tuple
  = "(" __ first:Expr rest:(__ "," __ e:Expr { return e, nil })+ __ ")" {
      return &ast.TupleExpr{
        Kind: "TupleExpr",
        Elems: sliceOf[ast.Expr](prepend(first, rest)),
        Loc: loc(c),
      }, nil
    }

SQLTimeValue
  = typ:(DATE / TIMESTAMP) _ s:StringLiteral {
        return &ast.SQLTimeValue {
          Kind: "SQLTimeValue",
          Type: typ.(string),
          Value: s.(*ast.Primitive),
          Loc: loc(c),
        }, nil
    }

/// === Literals ===

Literal
  = TypeLiteral
  / StringLiteral
  / FString
  / SubnetLiteral
  / AddressLiteral
  / BytesLiteral
  / Duration
  / Time
  / FloatLiteral
  / IntegerLiteral
  / BooleanLiteral
  / NullLiteral

SubnetLiteral
  = v:IP6Net !IdentifierRest {
      return newPrimitive(c, "net", v.(string)), nil
    }
  / v:IP4Net {
      return newPrimitive(c, "net", v.(string)), nil
    }

AddressLiteral
  = v:IP6 !IdentifierRest {
      return newPrimitive(c, "ip", v.(string)), nil
    }
  / v:IP {
      return newPrimitive(c, "ip", v.(string)), nil
    }

FloatLiteral
  = v:FloatString {
      return newPrimitive(c, "float64", v.(string)), nil
    }

IntegerLiteral
  = v:IntString {
      return newPrimitive(c, "int64", v.(string)), nil
    }

BooleanLiteral
  = TRUE  { return newPrimitive(c, "bool", "true"), nil }
  / FALSE { return newPrimitive(c, "bool", "false"), nil }

NullLiteral
  = NULL { return newPrimitive(c, "null", ""), nil }

BytesLiteral
  = "0x" HexDigit* {
    return newPrimitive(c, "bytes", string(c.text)), nil
  }

TypeLiteral
  = "<" typ:Type ">" {
      return &ast.TypeValue{
        Kind:"TypeValue",
        Value: typ.(ast.Type),
        Loc: loc(c),
      }, nil
    }

Type
  = AmbiguousType
  / ComplexType

AmbiguousType
  = name:PrimitiveType !IdentifierRest { return name, nil }
  / ERROR __ "(" __ t:Type __ ")" {
      return &ast.TypeError{
          Kind: "TypeError",
          Type: t.(ast.Type),
          Loc: loc(c),
      }, nil
    }
  / name:Name opt:(__ '=' __ Type)? {
      if opt != nil {
        return &ast.TypeDef{
            Kind: "TypeDef",
            Name: name.(*ast.Name).Text,
            Type: opt.([]any)[3].(ast.Type),
            Loc: loc(c),
        }, nil
      }
      return &ast.TypeName{Kind: "TypeName", Name: name.(*ast.Name).Text, Loc: loc(c)}, nil
    }
  / "(" __ types:TypeList ")" {
      return &ast.TypeUnion{
        Kind: "TypeUnion",
        Types: sliceOf[ast.Type](types),
        Loc: loc(c),
      }, nil
    }

TypeList
  = first:Type rest:TypeListTail+ {
    return prepend(first, rest), nil
  }

TypeListTail = __ "," __ typ:Type { return typ, nil }

ComplexType
  = "{" __ fields:TypeFieldList __ "}" {
      return &ast.TypeRecord{
          Kind: "TypeRecord",
          Fields: sliceOf[ast.TypeField](fields),
          Loc: loc(c),
      }, nil
    }
  / "[" __ typ:Type __ "]" {
      return &ast.TypeArray{
          Kind: "TypeArray",
          Type: typ.(ast.Type),
          Loc: loc(c),
      }, nil
    }
  / "|[" __ typ:Type __ "]|" {
      return &ast.TypeSet{
          Kind: "TypeSet",
          Type: typ.(ast.Type),
          Loc: loc(c),
      }, nil
    }
  / "|{" __ keyType:Type __ ":" __ valType:Type __ "}|" {
      return &ast.TypeMap{
        Kind: "TypeMap",
        KeyType: keyType.(ast.Type),
        ValType: valType.(ast.Type),
        Loc: loc(c),
      }, nil
    }

StringLiteral
  = s:DoubleQuotedString { return &ast.DoubleQuote{Kind:"DoubleQuote", Text: s.(string), Loc: loc(c)}, nil }
  / s:SingleQuotedString { return newPrimitive(c, "string", s.(string)), nil }

FString
  = "f\"" v:FStringDoubleQuotedElem* '"' {
      return &ast.FString{
        Kind: "FString",
        Elems: sliceOf[ast.FStringElem](v),
        Loc: loc(c),
      }, nil
    }
  / "f'" v:FStringSingleQuotedElem* "'" {
      return &ast.FString{
        Kind: "FString",
        Elems: sliceOf[ast.FStringElem](v),
        Loc: loc(c),
      }, nil
    }

FStringDoubleQuotedElem
  = FStringExpr
  / v:FStringDoubleQuotedChar+ {
      return &ast.FStringText{Kind: "FStringText", Loc: loc(c), Text: joinChars(v)}, nil
    }

FStringDoubleQuotedChar
  = "\\" v:"{" { return v, nil }
  / !("{") v:DoubleQuotedChar { return v, nil }

FStringSingleQuotedElem
  = FStringExpr
  / v:FStringSingleQuotedChar+ {
      return &ast.FStringText{Kind: "FStringText", Loc: loc(c), Text: joinChars(v)}, nil
    }

FStringSingleQuotedChar
  = "\\" v:"{" { return v, nil }
  / !("{") v:SingleQuotedChar { return v, nil }

FStringExpr
  = "{" __ e:Expr __ "}" {
      return &ast.FStringExpr{
        Kind: "FStringExpr",
        Expr: e.(ast.Expr),
        Loc: loc(c),
      }, nil
    }

PrimitiveType
  = (   "uint8" / "uint16" / "uint32" / "uint64"
      / "int8" / "int16" / "int32" / "int64"
      / "float16" / "float32" / "float64"
      / "bool" / "string"
      / "duration" / "time"
      / "bytes"
      / "ip" / "net"
      / "type" / "null") {
            return &ast.TypePrimitive{
                Kind: "TypePrimitive",
                Name: string(c.text),
                Loc: loc(c),
            }, nil
        }

TypeFieldList
  = first:TypeField rest:TypeFieldListTail* {
    return prepend(first, rest), nil
  }
  / "" { return nil, nil }

TypeFieldListTail = __ "," __ typ:TypeField { return typ, nil }

TypeField
  = name:Name __ ":" __ typ:Type {
      return ast.TypeField{
          Name: name.(*ast.Name).Text,
          Type: typ.(ast.Type),
          Loc: loc(c),
      }, nil
    }

Name
  = s:DottedIDs          { return &ast.Name{Kind:"Name",Text:s.(string),Loc:loc(c) }, nil }
  / s:IdentifierName     { return &ast.Name{Kind:"Name",Text:s.(string),Loc:loc(c) }, nil }
  / s:DoubleQuotedString { return &ast.Name{Kind:"Name",Text:s.(string),Loc:loc(c) }, nil }
  / s:SingleQuotedString { return &ast.Name{Kind:"Name",Text:s.(string),Loc:loc(c) }, nil }
  / s:KSUID              { return &ast.Name{Kind:"Name",Text:s.(string),Loc:loc(c) }, nil }

DottedIDs
  = (IdentifierStart / ".") (IdentifierRest / ".")* { return string(c.text), nil }

Identifier
  = id:IdentifierName {
      return &ast.ID{
          Kind: "ID",
          Name: id.(string),
          Loc: loc(c),
      }, nil
    }

Identifiers
  = first:Identifier rest:(__ "," __ name:Identifier { return name, nil })* {
    return prepend(first, rest), nil
  }

SQLIdentifier
  = Identifier
  / s:BacktickString     { return &ast.ID{Kind:"ID", Name:s.(string), Loc:loc(c) }, nil }
  / s:DoubleQuotedString { return &ast.ID{Kind:"ID", Name:s.(string), Loc:loc(c) }, nil }

IdentifierName
  = !(IDGuard !IdentifierRest) IdentifierStart IdentifierRest* { return string(c.text), nil }
  / "$" { return string(c.text), nil }
  / "\\" id:IDGuard { return id, nil }
  // "type" is a search guard but should not be an id guard
  / "type" { return string(c.text), nil }
  / BacktickString 

IdentifierStart
  = UnicodeLetter
  / "$"
  / "_"

IdentifierRest
  = IdentifierStart
  / UnicodeCombiningMark
  / UnicodeDigit
  / UnicodeConnectorPunctuation

IDGuard
  = BooleanLiteral
  / NullLiteral
  / NaN
  / Infinity

Time
  = FullDate "T" FullTime {
      return newPrimitive(c, "time", string(c.text)), nil
    }

FullDate = D4 "-" D2 "-" D2

D4 = [0-9][0-9][0-9][0-9]
D2 = [0-9][0-9]

FullTime = PartialTime TimeOffset

PartialTime = D2 ":" D2 ":" D2 ("." [0-9]+)?

TimeOffset
  = "Z"
  / ("+" / "-") D2 ":" D2 ("." [0-9]+)?

Duration
  = "-"? (Decimal TimeUnit)+ {
      return newPrimitive(c, "duration", string(c.text)), nil
    }

Decimal = UInt ("." UInt)?

TimeUnit
  = "ns"
  / "us"
  / "ms"
  / "s"
  / "m"
  / "h"
  / "d"
  / "w"
  / "y"

IP
  = UInt "." UInt "." UInt "." UInt { return string(c.text), nil }

IP6
  = !(Hex ":" Hex !(HexDigit / ":")) v:IP6Variations { return v, nil }

// this matches a superset of legal syntax for ip6 addresses but the compiler
// will catch any errors when translating the filter
IP6Variations
  = a:HexColon+ b:IP6Tail {
      return joinChars(a) + b.(string), nil
    }
  / a:Hex b:ColonHex* "::" d:HexColon* e:IP6Tail {
      return a.(string) + joinChars(b) + "::" + joinChars(d) + e.(string), nil
    }
  / "::" a:HexColon* b:IP6Tail {
      return "::" + joinChars(a) + b.(string), nil
    }
  / a:Hex b:ColonHex* "::" {
      return a.(string) + joinChars(b) + "::", nil
    }
  / "::" {
      return "::", nil
    }

IP6Tail
  = IP
  / Hex

ColonHex = ":" v:Hex { return ":" + v.(string), nil }

HexColon = v:Hex ":" { return v.(string) + ":", nil }

IP4Net
  = a:IP '/' m:UIntString {
      return a.(string) + "/" + m.(string), nil
    }

IP6Net
  = a:IP6 '/' m:UIntString {
      return a.(string) + "/" + m.(string), nil
    }

UInt
 = s:UIntString { return parseInt(s), nil }

IntString
  = UIntString
  / MinusIntString

UIntString = [0-9]+ { return string(c.text), nil }

MinusIntString
  = "-" UIntString { return string(c.text), nil }

FloatString
  = "-"? [0-9]+ "." [0-9]* ExponentPart? { return string(c.text), nil }
  / "-"? "." [0-9]+ ExponentPart? { return string(c.text), nil }
  / (NaN / Infinity) { return string(c.text), nil }


ExponentPart = "e"i [+-]? UIntString

NaN = "NaN"

Infinity = ("-" / "+")? "Inf"

Hex = HexDigit+ { return string(c.text), nil }

HexDigit = [0-9a-fA-F]

SingleQuotedString
  = "'" v:SingleQuotedChar* "'" { return joinChars(v), nil }

DoubleQuotedString
  = '"' v:DoubleQuotedChar* '"' { return joinChars(v), nil }

DoubleQuotedChar
  = !('"' / EscapedChar) . { return string(c.text), nil }
  / "\\" s:EscapeSequence { return s, nil }

BacktickString
  = '`' v:BacktickChar* '`' { return joinChars(v), nil }

BacktickChar
  = !('`' / EscapedChar) . { return string(c.text), nil }
  / "\\" s:EscapeSequence { return s, nil }

KeyWord
  = head:KeyWordStart tail:KeyWordRest* { return head.(string) + joinChars(tail), nil }

KeyWordStart
  = KeyWordChars
  / KeyWordEsc

KeyWordRest
  = KeyWordStart
  / [0-9]

KeyWordChars = (UnicodeLetter / [_.:/%#@~]) { return string(c.text), nil }

KeyWordEsc = "\\" s:(KeywordEscape / EscapeSequence) { return s, nil }

GlobPattern
  = &GlobProperStart &GlobHasStar head:GlobStart tail:GlobRest* {
      return head.(string) + joinChars(tail), nil
    }

GlobProperStart = KeyWordStart / "*"+ KeyWordRest
GlobHasStar = KeyWordRest* "*"

GlobStart
  = KeyWordChars
  / GlobEsc
  / "*" { return "*", nil }

GlobRest
  = GlobStart
  / [0-9]

GlobEsc = "\\" s:(GlobEscape / EscapeSequence) { return s, nil }

GlobEscape
  = "=" { return "=", nil }
  / "*" { return "\\*", nil }
  / [+-]

SingleQuotedChar
  = !("'" / EscapedChar) . { return string(c.text), nil }
  / "\\" s:EscapeSequence { return s, nil }

EscapeSequence
  = SingleCharEscape
  / UnicodeEscape

SingleCharEscape
  = "'"
  / "\"" { return string(c.text), nil }
  / "\\"
  / "b" { return "\b", nil }
  / "f" { return "\f", nil }
  / "n" { return "\n", nil }
  / "r" { return "\r", nil }
  / "t" { return "\t", nil }
  / "v" { return "\v", nil }

KeywordEscape
  = "=" { return "=", nil }
  / "*" { return "*", nil }
  / [+-]

UnicodeEscape
  = "u" chars:(HexDigit HexDigit HexDigit HexDigit) {
      return makeUnicodeChar(chars), nil
    }
  / "u" "{" chars:(HexDigit HexDigit? HexDigit? HexDigit? HexDigit? HexDigit?) "}" {
      return makeUnicodeChar(chars), nil
    }

RegexpPattern
  = "/" body:RegexpBody "/" !KeyWordStart { return body, nil }

RegexpBody
  = ([^/\\] / "\\" .)+ { return string(c.text), nil }

EscapedChar
  = [\x00-\x1f\\]

_ = AnySpace+

__ = AnySpace*

AnySpace
  = WhiteSpace
  / LineTerminator
  / Comment

UnicodeLetter
  = Lu
  / Ll
  / Lt
  / Lm
  / Lo
  / Nl

UnicodeCombiningMark
  = Mn
  / Mc

UnicodeDigit
  = Nd

UnicodeConnectorPunctuation
  = Pc

// Unicode Character Categories
// Taken from: https://github.com/pegjs/pegjs/blob/b7b87ea8aeeaa1caf096e2da99fd95a971890ca1/examples/javascript.pegjs#L374

// Letter, Lowercase
Ll = [\u0061-\u007A\u00B5\u00DF-\u00F6\u00F8-\u00FF\u0101\u0103\u0105\u0107\u0109\u010B\u010D\u010F\u0111\u0113\u0115\u0117\u0119\u011B\u011D\u011F\u0121\u0123\u0125\u0127\u0129\u012B\u012D\u012F\u0131\u0133\u0135\u0137-\u0138\u013A\u013C\u013E\u0140\u0142\u0144\u0146\u0148-\u0149\u014B\u014D\u014F\u0151\u0153\u0155\u0157\u0159\u015B\u015D\u015F\u0161\u0163\u0165\u0167\u0169\u016B\u016D\u016F\u0171\u0173\u0175\u0177\u017A\u017C\u017E-\u0180\u0183\u0185\u0188\u018C-\u018D\u0192\u0195\u0199-\u019B\u019E\u01A1\u01A3\u01A5\u01A8\u01AA-\u01AB\u01AD\u01B0\u01B4\u01B6\u01B9-\u01BA\u01BD-\u01BF\u01C6\u01C9\u01CC\u01CE\u01D0\u01D2\u01D4\u01D6\u01D8\u01DA\u01DC-\u01DD\u01DF\u01E1\u01E3\u01E5\u01E7\u01E9\u01EB\u01ED\u01EF-\u01F0\u01F3\u01F5\u01F9\u01FB\u01FD\u01FF\u0201\u0203\u0205\u0207\u0209\u020B\u020D\u020F\u0211\u0213\u0215\u0217\u0219\u021B\u021D\u021F\u0221\u0223\u0225\u0227\u0229\u022B\u022D\u022F\u0231\u0233-\u0239\u023C\u023F-\u0240\u0242\u0247\u0249\u024B\u024D\u024F-\u0293\u0295-\u02AF\u0371\u0373\u0377\u037B-\u037D\u0390\u03AC-\u03CE\u03D0-\u03D1\u03D5-\u03D7\u03D9\u03DB\u03DD\u03DF\u03E1\u03E3\u03E5\u03E7\u03E9\u03EB\u03ED\u03EF-\u03F3\u03F5\u03F8\u03FB-\u03FC\u0430-\u045F\u0461\u0463\u0465\u0467\u0469\u046B\u046D\u046F\u0471\u0473\u0475\u0477\u0479\u047B\u047D\u047F\u0481\u048B\u048D\u048F\u0491\u0493\u0495\u0497\u0499\u049B\u049D\u049F\u04A1\u04A3\u04A5\u04A7\u04A9\u04AB\u04AD\u04AF\u04B1\u04B3\u04B5\u04B7\u04B9\u04BB\u04BD\u04BF\u04C2\u04C4\u04C6\u04C8\u04CA\u04CC\u04CE-\u04CF\u04D1\u04D3\u04D5\u04D7\u04D9\u04DB\u04DD\u04DF\u04E1\u04E3\u04E5\u04E7\u04E9\u04EB\u04ED\u04EF\u04F1\u04F3\u04F5\u04F7\u04F9\u04FB\u04FD\u04FF\u0501\u0503\u0505\u0507\u0509\u050B\u050D\u050F\u0511\u0513\u0515\u0517\u0519\u051B\u051D\u051F\u0521\u0523\u0525\u0527\u0529\u052B\u052D\u052F\u0560-\u0588\u10D0-\u10FA\u10FD-\u10FF\u13F8-\u13FD\u1C80-\u1C88\u1D00-\u1D2B\u1D6B-\u1D77\u1D79-\u1D9A\u1E01\u1E03\u1E05\u1E07\u1E09\u1E0B\u1E0D\u1E0F\u1E11\u1E13\u1E15\u1E17\u1E19\u1E1B\u1E1D\u1E1F\u1E21\u1E23\u1E25\u1E27\u1E29\u1E2B\u1E2D\u1E2F\u1E31\u1E33\u1E35\u1E37\u1E39\u1E3B\u1E3D\u1E3F\u1E41\u1E43\u1E45\u1E47\u1E49\u1E4B\u1E4D\u1E4F\u1E51\u1E53\u1E55\u1E57\u1E59\u1E5B\u1E5D\u1E5F\u1E61\u1E63\u1E65\u1E67\u1E69\u1E6B\u1E6D\u1E6F\u1E71\u1E73\u1E75\u1E77\u1E79\u1E7B\u1E7D\u1E7F\u1E81\u1E83\u1E85\u1E87\u1E89\u1E8B\u1E8D\u1E8F\u1E91\u1E93\u1E95-\u1E9D\u1E9F\u1EA1\u1EA3\u1EA5\u1EA7\u1EA9\u1EAB\u1EAD\u1EAF\u1EB1\u1EB3\u1EB5\u1EB7\u1EB9\u1EBB\u1EBD\u1EBF\u1EC1\u1EC3\u1EC5\u1EC7\u1EC9\u1ECB\u1ECD\u1ECF\u1ED1\u1ED3\u1ED5\u1ED7\u1ED9\u1EDB\u1EDD\u1EDF\u1EE1\u1EE3\u1EE5\u1EE7\u1EE9\u1EEB\u1EED\u1EEF\u1EF1\u1EF3\u1EF5\u1EF7\u1EF9\u1EFB\u1EFD\u1EFF-\u1F07\u1F10-\u1F15\u1F20-\u1F27\u1F30-\u1F37\u1F40-\u1F45\u1F50-\u1F57\u1F60-\u1F67\u1F70-\u1F7D\u1F80-\u1F87\u1F90-\u1F97\u1FA0-\u1FA7\u1FB0-\u1FB4\u1FB6-\u1FB7\u1FBE\u1FC2-\u1FC4\u1FC6-\u1FC7\u1FD0-\u1FD3\u1FD6-\u1FD7\u1FE0-\u1FE7\u1FF2-\u1FF4\u1FF6-\u1FF7\u210A\u210E-\u210F\u2113\u212F\u2134\u2139\u213C-\u213D\u2146-\u2149\u214E\u2184\u2C30-\u2C5E\u2C61\u2C65-\u2C66\u2C68\u2C6A\u2C6C\u2C71\u2C73-\u2C74\u2C76-\u2C7B\u2C81\u2C83\u2C85\u2C87\u2C89\u2C8B\u2C8D\u2C8F\u2C91\u2C93\u2C95\u2C97\u2C99\u2C9B\u2C9D\u2C9F\u2CA1\u2CA3\u2CA5\u2CA7\u2CA9\u2CAB\u2CAD\u2CAF\u2CB1\u2CB3\u2CB5\u2CB7\u2CB9\u2CBB\u2CBD\u2CBF\u2CC1\u2CC3\u2CC5\u2CC7\u2CC9\u2CCB\u2CCD\u2CCF\u2CD1\u2CD3\u2CD5\u2CD7\u2CD9\u2CDB\u2CDD\u2CDF\u2CE1\u2CE3-\u2CE4\u2CEC\u2CEE\u2CF3\u2D00-\u2D25\u2D27\u2D2D\uA641\uA643\uA645\uA647\uA649\uA64B\uA64D\uA64F\uA651\uA653\uA655\uA657\uA659\uA65B\uA65D\uA65F\uA661\uA663\uA665\uA667\uA669\uA66B\uA66D\uA681\uA683\uA685\uA687\uA689\uA68B\uA68D\uA68F\uA691\uA693\uA695\uA697\uA699\uA69B\uA723\uA725\uA727\uA729\uA72B\uA72D\uA72F-\uA731\uA733\uA735\uA737\uA739\uA73B\uA73D\uA73F\uA741\uA743\uA745\uA747\uA749\uA74B\uA74D\uA74F\uA751\uA753\uA755\uA757\uA759\uA75B\uA75D\uA75F\uA761\uA763\uA765\uA767\uA769\uA76B\uA76D\uA76F\uA771-\uA778\uA77A\uA77C\uA77F\uA781\uA783\uA785\uA787\uA78C\uA78E\uA791\uA793-\uA795\uA797\uA799\uA79B\uA79D\uA79F\uA7A1\uA7A3\uA7A5\uA7A7\uA7A9\uA7AF\uA7B5\uA7B7\uA7B9\uA7FA\uAB30-\uAB5A\uAB60-\uAB65\uAB70-\uABBF\uFB00-\uFB06\uFB13-\uFB17\uFF41-\uFF5A]

// Letter, Modifier
Lm = [\u02B0-\u02C1\u02C6-\u02D1\u02E0-\u02E4\u02EC\u02EE\u0374\u037A\u0559\u0640\u06E5-\u06E6\u07F4-\u07F5\u07FA\u081A\u0824\u0828\u0971\u0E46\u0EC6\u10FC\u17D7\u1843\u1AA7\u1C78-\u1C7D\u1D2C-\u1D6A\u1D78\u1D9B-\u1DBF\u2071\u207F\u2090-\u209C\u2C7C-\u2C7D\u2D6F\u2E2F\u3005\u3031-\u3035\u303B\u309D-\u309E\u30FC-\u30FE\uA015\uA4F8-\uA4FD\uA60C\uA67F\uA69C-\uA69D\uA717-\uA71F\uA770\uA788\uA7F8-\uA7F9\uA9CF\uA9E6\uAA70\uAADD\uAAF3-\uAAF4\uAB5C-\uAB5F\uFF70\uFF9E-\uFF9F]

// Letter, Other
Lo = [\u00AA\u00BA\u01BB\u01C0-\u01C3\u0294\u05D0-\u05EA\u05EF-\u05F2\u0620-\u063F\u0641-\u064A\u066E-\u066F\u0671-\u06D3\u06D5\u06EE-\u06EF\u06FA-\u06FC\u06FF\u0710\u0712-\u072F\u074D-\u07A5\u07B1\u07CA-\u07EA\u0800-\u0815\u0840-\u0858\u0860-\u086A\u08A0-\u08B4\u08B6-\u08BD\u0904-\u0939\u093D\u0950\u0958-\u0961\u0972-\u0980\u0985-\u098C\u098F-\u0990\u0993-\u09A8\u09AA-\u09B0\u09B2\u09B6-\u09B9\u09BD\u09CE\u09DC-\u09DD\u09DF-\u09E1\u09F0-\u09F1\u09FC\u0A05-\u0A0A\u0A0F-\u0A10\u0A13-\u0A28\u0A2A-\u0A30\u0A32-\u0A33\u0A35-\u0A36\u0A38-\u0A39\u0A59-\u0A5C\u0A5E\u0A72-\u0A74\u0A85-\u0A8D\u0A8F-\u0A91\u0A93-\u0AA8\u0AAA-\u0AB0\u0AB2-\u0AB3\u0AB5-\u0AB9\u0ABD\u0AD0\u0AE0-\u0AE1\u0AF9\u0B05-\u0B0C\u0B0F-\u0B10\u0B13-\u0B28\u0B2A-\u0B30\u0B32-\u0B33\u0B35-\u0B39\u0B3D\u0B5C-\u0B5D\u0B5F-\u0B61\u0B71\u0B83\u0B85-\u0B8A\u0B8E-\u0B90\u0B92-\u0B95\u0B99-\u0B9A\u0B9C\u0B9E-\u0B9F\u0BA3-\u0BA4\u0BA8-\u0BAA\u0BAE-\u0BB9\u0BD0\u0C05-\u0C0C\u0C0E-\u0C10\u0C12-\u0C28\u0C2A-\u0C39\u0C3D\u0C58-\u0C5A\u0C60-\u0C61\u0C80\u0C85-\u0C8C\u0C8E-\u0C90\u0C92-\u0CA8\u0CAA-\u0CB3\u0CB5-\u0CB9\u0CBD\u0CDE\u0CE0-\u0CE1\u0CF1-\u0CF2\u0D05-\u0D0C\u0D0E-\u0D10\u0D12-\u0D3A\u0D3D\u0D4E\u0D54-\u0D56\u0D5F-\u0D61\u0D7A-\u0D7F\u0D85-\u0D96\u0D9A-\u0DB1\u0DB3-\u0DBB\u0DBD\u0DC0-\u0DC6\u0E01-\u0E30\u0E32-\u0E33\u0E40-\u0E45\u0E81-\u0E82\u0E84\u0E87-\u0E88\u0E8A\u0E8D\u0E94-\u0E97\u0E99-\u0E9F\u0EA1-\u0EA3\u0EA5\u0EA7\u0EAA-\u0EAB\u0EAD-\u0EB0\u0EB2-\u0EB3\u0EBD\u0EC0-\u0EC4\u0EDC-\u0EDF\u0F00\u0F40-\u0F47\u0F49-\u0F6C\u0F88-\u0F8C\u1000-\u102A\u103F\u1050-\u1055\u105A-\u105D\u1061\u1065-\u1066\u106E-\u1070\u1075-\u1081\u108E\u1100-\u1248\u124A-\u124D\u1250-\u1256\u1258\u125A-\u125D\u1260-\u1288\u128A-\u128D\u1290-\u12B0\u12B2-\u12B5\u12B8-\u12BE\u12C0\u12C2-\u12C5\u12C8-\u12D6\u12D8-\u1310\u1312-\u1315\u1318-\u135A\u1380-\u138F\u1401-\u166C\u166F-\u167F\u1681-\u169A\u16A0-\u16EA\u16F1-\u16F8\u1700-\u170C\u170E-\u1711\u1720-\u1731\u1740-\u1751\u1760-\u176C\u176E-\u1770\u1780-\u17B3\u17DC\u1820-\u1842\u1844-\u1878\u1880-\u1884\u1887-\u18A8\u18AA\u18B0-\u18F5\u1900-\u191E\u1950-\u196D\u1970-\u1974\u1980-\u19AB\u19B0-\u19C9\u1A00-\u1A16\u1A20-\u1A54\u1B05-\u1B33\u1B45-\u1B4B\u1B83-\u1BA0\u1BAE-\u1BAF\u1BBA-\u1BE5\u1C00-\u1C23\u1C4D-\u1C4F\u1C5A-\u1C77\u1CE9-\u1CEC\u1CEE-\u1CF1\u1CF5-\u1CF6\u2135-\u2138\u2D30-\u2D67\u2D80-\u2D96\u2DA0-\u2DA6\u2DA8-\u2DAE\u2DB0-\u2DB6\u2DB8-\u2DBE\u2DC0-\u2DC6\u2DC8-\u2DCE\u2DD0-\u2DD6\u2DD8-\u2DDE\u3006\u303C\u3041-\u3096\u309F\u30A1-\u30FA\u30FF\u3105-\u312F\u3131-\u318E\u31A0-\u31BA\u31F0-\u31FF\u3400-\u4DB5\u4E00-\u9FEF\uA000-\uA014\uA016-\uA48C\uA4D0-\uA4F7\uA500-\uA60B\uA610-\uA61F\uA62A-\uA62B\uA66E\uA6A0-\uA6E5\uA78F\uA7F7\uA7FB-\uA801\uA803-\uA805\uA807-\uA80A\uA80C-\uA822\uA840-\uA873\uA882-\uA8B3\uA8F2-\uA8F7\uA8FB\uA8FD-\uA8FE\uA90A-\uA925\uA930-\uA946\uA960-\uA97C\uA984-\uA9B2\uA9E0-\uA9E4\uA9E7-\uA9EF\uA9FA-\uA9FE\uAA00-\uAA28\uAA40-\uAA42\uAA44-\uAA4B\uAA60-\uAA6F\uAA71-\uAA76\uAA7A\uAA7E-\uAAAF\uAAB1\uAAB5-\uAAB6\uAAB9-\uAABD\uAAC0\uAAC2\uAADB-\uAADC\uAAE0-\uAAEA\uAAF2\uAB01-\uAB06\uAB09-\uAB0E\uAB11-\uAB16\uAB20-\uAB26\uAB28-\uAB2E\uABC0-\uABE2\uAC00-\uD7A3\uD7B0-\uD7C6\uD7CB-\uD7FB\uF900-\uFA6D\uFA70-\uFAD9\uFB1D\uFB1F-\uFB28\uFB2A-\uFB36\uFB38-\uFB3C\uFB3E\uFB40-\uFB41\uFB43-\uFB44\uFB46-\uFBB1\uFBD3-\uFD3D\uFD50-\uFD8F\uFD92-\uFDC7\uFDF0-\uFDFB\uFE70-\uFE74\uFE76-\uFEFC\uFF66-\uFF6F\uFF71-\uFF9D\uFFA0-\uFFBE\uFFC2-\uFFC7\uFFCA-\uFFCF\uFFD2-\uFFD7\uFFDA-\uFFDC]

// Letter, Titlecase
Lt = [\u01C5\u01C8\u01CB\u01F2\u1F88-\u1F8F\u1F98-\u1F9F\u1FA8-\u1FAF\u1FBC\u1FCC\u1FFC]

// Letter, Uppercase
Lu = [\u0041-\u005A\u00C0-\u00D6\u00D8-\u00DE\u0100\u0102\u0104\u0106\u0108\u010A\u010C\u010E\u0110\u0112\u0114\u0116\u0118\u011A\u011C\u011E\u0120\u0122\u0124\u0126\u0128\u012A\u012C\u012E\u0130\u0132\u0134\u0136\u0139\u013B\u013D\u013F\u0141\u0143\u0145\u0147\u014A\u014C\u014E\u0150\u0152\u0154\u0156\u0158\u015A\u015C\u015E\u0160\u0162\u0164\u0166\u0168\u016A\u016C\u016E\u0170\u0172\u0174\u0176\u0178-\u0179\u017B\u017D\u0181-\u0182\u0184\u0186-\u0187\u0189-\u018B\u018E-\u0191\u0193-\u0194\u0196-\u0198\u019C-\u019D\u019F-\u01A0\u01A2\u01A4\u01A6-\u01A7\u01A9\u01AC\u01AE-\u01AF\u01B1-\u01B3\u01B5\u01B7-\u01B8\u01BC\u01C4\u01C7\u01CA\u01CD\u01CF\u01D1\u01D3\u01D5\u01D7\u01D9\u01DB\u01DE\u01E0\u01E2\u01E4\u01E6\u01E8\u01EA\u01EC\u01EE\u01F1\u01F4\u01F6-\u01F8\u01FA\u01FC\u01FE\u0200\u0202\u0204\u0206\u0208\u020A\u020C\u020E\u0210\u0212\u0214\u0216\u0218\u021A\u021C\u021E\u0220\u0222\u0224\u0226\u0228\u022A\u022C\u022E\u0230\u0232\u023A-\u023B\u023D-\u023E\u0241\u0243-\u0246\u0248\u024A\u024C\u024E\u0370\u0372\u0376\u037F\u0386\u0388-\u038A\u038C\u038E-\u038F\u0391-\u03A1\u03A3-\u03AB\u03CF\u03D2-\u03D4\u03D8\u03DA\u03DC\u03DE\u03E0\u03E2\u03E4\u03E6\u03E8\u03EA\u03EC\u03EE\u03F4\u03F7\u03F9-\u03FA\u03FD-\u042F\u0460\u0462\u0464\u0466\u0468\u046A\u046C\u046E\u0470\u0472\u0474\u0476\u0478\u047A\u047C\u047E\u0480\u048A\u048C\u048E\u0490\u0492\u0494\u0496\u0498\u049A\u049C\u049E\u04A0\u04A2\u04A4\u04A6\u04A8\u04AA\u04AC\u04AE\u04B0\u04B2\u04B4\u04B6\u04B8\u04BA\u04BC\u04BE\u04C0-\u04C1\u04C3\u04C5\u04C7\u04C9\u04CB\u04CD\u04D0\u04D2\u04D4\u04D6\u04D8\u04DA\u04DC\u04DE\u04E0\u04E2\u04E4\u04E6\u04E8\u04EA\u04EC\u04EE\u04F0\u04F2\u04F4\u04F6\u04F8\u04FA\u04FC\u04FE\u0500\u0502\u0504\u0506\u0508\u050A\u050C\u050E\u0510\u0512\u0514\u0516\u0518\u051A\u051C\u051E\u0520\u0522\u0524\u0526\u0528\u052A\u052C\u052E\u0531-\u0556\u10A0-\u10C5\u10C7\u10CD\u13A0-\u13F5\u1C90-\u1CBA\u1CBD-\u1CBF\u1E00\u1E02\u1E04\u1E06\u1E08\u1E0A\u1E0C\u1E0E\u1E10\u1E12\u1E14\u1E16\u1E18\u1E1A\u1E1C\u1E1E\u1E20\u1E22\u1E24\u1E26\u1E28\u1E2A\u1E2C\u1E2E\u1E30\u1E32\u1E34\u1E36\u1E38\u1E3A\u1E3C\u1E3E\u1E40\u1E42\u1E44\u1E46\u1E48\u1E4A\u1E4C\u1E4E\u1E50\u1E52\u1E54\u1E56\u1E58\u1E5A\u1E5C\u1E5E\u1E60\u1E62\u1E64\u1E66\u1E68\u1E6A\u1E6C\u1E6E\u1E70\u1E72\u1E74\u1E76\u1E78\u1E7A\u1E7C\u1E7E\u1E80\u1E82\u1E84\u1E86\u1E88\u1E8A\u1E8C\u1E8E\u1E90\u1E92\u1E94\u1E9E\u1EA0\u1EA2\u1EA4\u1EA6\u1EA8\u1EAA\u1EAC\u1EAE\u1EB0\u1EB2\u1EB4\u1EB6\u1EB8\u1EBA\u1EBC\u1EBE\u1EC0\u1EC2\u1EC4\u1EC6\u1EC8\u1ECA\u1ECC\u1ECE\u1ED0\u1ED2\u1ED4\u1ED6\u1ED8\u1EDA\u1EDC\u1EDE\u1EE0\u1EE2\u1EE4\u1EE6\u1EE8\u1EEA\u1EEC\u1EEE\u1EF0\u1EF2\u1EF4\u1EF6\u1EF8\u1EFA\u1EFC\u1EFE\u1F08-\u1F0F\u1F18-\u1F1D\u1F28-\u1F2F\u1F38-\u1F3F\u1F48-\u1F4D\u1F59\u1F5B\u1F5D\u1F5F\u1F68-\u1F6F\u1FB8-\u1FBB\u1FC8-\u1FCB\u1FD8-\u1FDB\u1FE8-\u1FEC\u1FF8-\u1FFB\u2102\u2107\u210B-\u210D\u2110-\u2112\u2115\u2119-\u211D\u2124\u2126\u2128\u212A-\u212D\u2130-\u2133\u213E-\u213F\u2145\u2183\u2C00-\u2C2E\u2C60\u2C62-\u2C64\u2C67\u2C69\u2C6B\u2C6D-\u2C70\u2C72\u2C75\u2C7E-\u2C80\u2C82\u2C84\u2C86\u2C88\u2C8A\u2C8C\u2C8E\u2C90\u2C92\u2C94\u2C96\u2C98\u2C9A\u2C9C\u2C9E\u2CA0\u2CA2\u2CA4\u2CA6\u2CA8\u2CAA\u2CAC\u2CAE\u2CB0\u2CB2\u2CB4\u2CB6\u2CB8\u2CBA\u2CBC\u2CBE\u2CC0\u2CC2\u2CC4\u2CC6\u2CC8\u2CCA\u2CCC\u2CCE\u2CD0\u2CD2\u2CD4\u2CD6\u2CD8\u2CDA\u2CDC\u2CDE\u2CE0\u2CE2\u2CEB\u2CED\u2CF2\uA640\uA642\uA644\uA646\uA648\uA64A\uA64C\uA64E\uA650\uA652\uA654\uA656\uA658\uA65A\uA65C\uA65E\uA660\uA662\uA664\uA666\uA668\uA66A\uA66C\uA680\uA682\uA684\uA686\uA688\uA68A\uA68C\uA68E\uA690\uA692\uA694\uA696\uA698\uA69A\uA722\uA724\uA726\uA728\uA72A\uA72C\uA72E\uA732\uA734\uA736\uA738\uA73A\uA73C\uA73E\uA740\uA742\uA744\uA746\uA748\uA74A\uA74C\uA74E\uA750\uA752\uA754\uA756\uA758\uA75A\uA75C\uA75E\uA760\uA762\uA764\uA766\uA768\uA76A\uA76C\uA76E\uA779\uA77B\uA77D-\uA77E\uA780\uA782\uA784\uA786\uA78B\uA78D\uA790\uA792\uA796\uA798\uA79A\uA79C\uA79E\uA7A0\uA7A2\uA7A4\uA7A6\uA7A8\uA7AA-\uA7AE\uA7B0-\uA7B4\uA7B6\uA7B8\uFF21-\uFF3A]

// Mark, Spacing Combining
Mc = [\u0903\u093B\u093E-\u0940\u0949-\u094C\u094E-\u094F\u0982-\u0983\u09BE-\u09C0\u09C7-\u09C8\u09CB-\u09CC\u09D7\u0A03\u0A3E-\u0A40\u0A83\u0ABE-\u0AC0\u0AC9\u0ACB-\u0ACC\u0B02-\u0B03\u0B3E\u0B40\u0B47-\u0B48\u0B4B-\u0B4C\u0B57\u0BBE-\u0BBF\u0BC1-\u0BC2\u0BC6-\u0BC8\u0BCA-\u0BCC\u0BD7\u0C01-\u0C03\u0C41-\u0C44\u0C82-\u0C83\u0CBE\u0CC0-\u0CC4\u0CC7-\u0CC8\u0CCA-\u0CCB\u0CD5-\u0CD6\u0D02-\u0D03\u0D3E-\u0D40\u0D46-\u0D48\u0D4A-\u0D4C\u0D57\u0D82-\u0D83\u0DCF-\u0DD1\u0DD8-\u0DDF\u0DF2-\u0DF3\u0F3E-\u0F3F\u0F7F\u102B-\u102C\u1031\u1038\u103B-\u103C\u1056-\u1057\u1062-\u1064\u1067-\u106D\u1083-\u1084\u1087-\u108C\u108F\u109A-\u109C\u17B6\u17BE-\u17C5\u17C7-\u17C8\u1923-\u1926\u1929-\u192B\u1930-\u1931\u1933-\u1938\u1A19-\u1A1A\u1A55\u1A57\u1A61\u1A63-\u1A64\u1A6D-\u1A72\u1B04\u1B35\u1B3B\u1B3D-\u1B41\u1B43-\u1B44\u1B82\u1BA1\u1BA6-\u1BA7\u1BAA\u1BE7\u1BEA-\u1BEC\u1BEE\u1BF2-\u1BF3\u1C24-\u1C2B\u1C34-\u1C35\u1CE1\u1CF2-\u1CF3\u1CF7\u302E-\u302F\uA823-\uA824\uA827\uA880-\uA881\uA8B4-\uA8C3\uA952-\uA953\uA983\uA9B4-\uA9B5\uA9BA-\uA9BB\uA9BD-\uA9C0\uAA2F-\uAA30\uAA33-\uAA34\uAA4D\uAA7B\uAA7D\uAAEB\uAAEE-\uAAEF\uAAF5\uABE3-\uABE4\uABE6-\uABE7\uABE9-\uABEA\uABEC]

// Mark, Nonspacing
Mn = [\u0300-\u036F\u0483-\u0487\u0591-\u05BD\u05BF\u05C1-\u05C2\u05C4-\u05C5\u05C7\u0610-\u061A\u064B-\u065F\u0670\u06D6-\u06DC\u06DF-\u06E4\u06E7-\u06E8\u06EA-\u06ED\u0711\u0730-\u074A\u07A6-\u07B0\u07EB-\u07F3\u07FD\u0816-\u0819\u081B-\u0823\u0825-\u0827\u0829-\u082D\u0859-\u085B\u08D3-\u08E1\u08E3-\u0902\u093A\u093C\u0941-\u0948\u094D\u0951-\u0957\u0962-\u0963\u0981\u09BC\u09C1-\u09C4\u09CD\u09E2-\u09E3\u09FE\u0A01-\u0A02\u0A3C\u0A41-\u0A42\u0A47-\u0A48\u0A4B-\u0A4D\u0A51\u0A70-\u0A71\u0A75\u0A81-\u0A82\u0ABC\u0AC1-\u0AC5\u0AC7-\u0AC8\u0ACD\u0AE2-\u0AE3\u0AFA-\u0AFF\u0B01\u0B3C\u0B3F\u0B41-\u0B44\u0B4D\u0B56\u0B62-\u0B63\u0B82\u0BC0\u0BCD\u0C00\u0C04\u0C3E-\u0C40\u0C46-\u0C48\u0C4A-\u0C4D\u0C55-\u0C56\u0C62-\u0C63\u0C81\u0CBC\u0CBF\u0CC6\u0CCC-\u0CCD\u0CE2-\u0CE3\u0D00-\u0D01\u0D3B-\u0D3C\u0D41-\u0D44\u0D4D\u0D62-\u0D63\u0DCA\u0DD2-\u0DD4\u0DD6\u0E31\u0E34-\u0E3A\u0E47-\u0E4E\u0EB1\u0EB4-\u0EB9\u0EBB-\u0EBC\u0EC8-\u0ECD\u0F18-\u0F19\u0F35\u0F37\u0F39\u0F71-\u0F7E\u0F80-\u0F84\u0F86-\u0F87\u0F8D-\u0F97\u0F99-\u0FBC\u0FC6\u102D-\u1030\u1032-\u1037\u1039-\u103A\u103D-\u103E\u1058-\u1059\u105E-\u1060\u1071-\u1074\u1082\u1085-\u1086\u108D\u109D\u135D-\u135F\u1712-\u1714\u1732-\u1734\u1752-\u1753\u1772-\u1773\u17B4-\u17B5\u17B7-\u17BD\u17C6\u17C9-\u17D3\u17DD\u180B-\u180D\u1885-\u1886\u18A9\u1920-\u1922\u1927-\u1928\u1932\u1939-\u193B\u1A17-\u1A18\u1A1B\u1A56\u1A58-\u1A5E\u1A60\u1A62\u1A65-\u1A6C\u1A73-\u1A7C\u1A7F\u1AB0-\u1ABD\u1B00-\u1B03\u1B34\u1B36-\u1B3A\u1B3C\u1B42\u1B6B-\u1B73\u1B80-\u1B81\u1BA2-\u1BA5\u1BA8-\u1BA9\u1BAB-\u1BAD\u1BE6\u1BE8-\u1BE9\u1BED\u1BEF-\u1BF1\u1C2C-\u1C33\u1C36-\u1C37\u1CD0-\u1CD2\u1CD4-\u1CE0\u1CE2-\u1CE8\u1CED\u1CF4\u1CF8-\u1CF9\u1DC0-\u1DF9\u1DFB-\u1DFF\u20D0-\u20DC\u20E1\u20E5-\u20F0\u2CEF-\u2CF1\u2D7F\u2DE0-\u2DFF\u302A-\u302D\u3099-\u309A\uA66F\uA674-\uA67D\uA69E-\uA69F\uA6F0-\uA6F1\uA802\uA806\uA80B\uA825-\uA826\uA8C4-\uA8C5\uA8E0-\uA8F1\uA8FF\uA926-\uA92D\uA947-\uA951\uA980-\uA982\uA9B3\uA9B6-\uA9B9\uA9BC\uA9E5\uAA29-\uAA2E\uAA31-\uAA32\uAA35-\uAA36\uAA43\uAA4C\uAA7C\uAAB0\uAAB2-\uAAB4\uAAB7-\uAAB8\uAABE-\uAABF\uAAC1\uAAEC-\uAAED\uAAF6\uABE5\uABE8\uABED\uFB1E\uFE00-\uFE0F\uFE20-\uFE2F]

// Number, Decimal Digit
Nd = [\u0030-\u0039\u0660-\u0669\u06F0-\u06F9\u07C0-\u07C9\u0966-\u096F\u09E6-\u09EF\u0A66-\u0A6F\u0AE6-\u0AEF\u0B66-\u0B6F\u0BE6-\u0BEF\u0C66-\u0C6F\u0CE6-\u0CEF\u0D66-\u0D6F\u0DE6-\u0DEF\u0E50-\u0E59\u0ED0-\u0ED9\u0F20-\u0F29\u1040-\u1049\u1090-\u1099\u17E0-\u17E9\u1810-\u1819\u1946-\u194F\u19D0-\u19D9\u1A80-\u1A89\u1A90-\u1A99\u1B50-\u1B59\u1BB0-\u1BB9\u1C40-\u1C49\u1C50-\u1C59\uA620-\uA629\uA8D0-\uA8D9\uA900-\uA909\uA9D0-\uA9D9\uA9F0-\uA9F9\uAA50-\uAA59\uABF0-\uABF9\uFF10-\uFF19]

// Number, Letter
Nl = [\u16EE-\u16F0\u2160-\u2182\u2185-\u2188\u3007\u3021-\u3029\u3038-\u303A\uA6E6-\uA6EF]

// Punctuation, Connector
Pc = [\u005F\u203F-\u2040\u2054\uFE33-\uFE34\uFE4D-\uFE4F\uFF3F]

// Separator, Space
Zs = [\u0020\u00A0\u1680\u2000-\u200A\u202F\u205F\u3000]

SourceCharacter
  = .

WhiteSpace "whitespace"
  = "\t"
  / "\v"
  / "\f"
  / " "
  / "\u00A0"
  / "\uFEFF"
  / Zs

LineTerminator
  = [\n\r\u2028\u2029]

// XXX We will leave multi-line comments out for now since there is some work
// that needs to be done disambiguating among KeyWord, Regexp, and "/*".
// We will tackle this in the search-expr branch that is changing the grammar
// to better mix together ad hoc keyword search with formal boolean expressions.
Comment "comment"
  // = MultiLineComment
  // / SingleLineComment
  = SingleLineComment

MultiLineComment
  = "/*" (!"*/" SourceCharacter)* "*/"

SingleLineComment
  = "//" (!LineTerminator SourceCharacter)*
  / "--" (!LineTerminator SourceCharacter)*

EOL = WhiteSpace* LineTerminator

EOT = _ / EOF

EOF = !.

EOKW = !KeyWordChars

/// === SuperSQL ===

SQLPipe 
  = s:Seq {
      return &ast.SQLPipe{
            Kind: "SQLPipe",
            Ops: sliceOf[ast.Op](s),
            Loc: loc(c),
        }, nil
    }

SelectOp = !"(" op:SelectExpr { return op, nil}

SelectExpr 
  = with:OptWithClause
    body:(
        SetOperation
      / Select
      / FromSelect
      / Values
      / "(" __ s:SQLPipe __")"      { return s, nil }
    )
    orderby:OptOrderByClause
    loff:OptSQLLimitOffset {
      op := body.(ast.Op)
      if with != nil {
          w := with.(*ast.With)
          w.Body = op
          op = w 
      }
      if orderby != nil {
          o := orderby.(*ast.OrderBy)
          o.Op = op
          op = o
      }
      if loff != nil {
          o := loff.(*ast.SQLLimitOffset)
          o.Op = op
          op = o
      }
      return op, nil
    }

Select 
  = SELECT 
    distinct:OptDistinct
    value:OptSelectValue
    //XXX for whitespace in front of target need it only if it begins with 
    // an identifier, e.g., select(1); is ok
    _ selection:Selection
    from:OptFromClause
    where:OptWhereClause
    group:OptGroupClause
    having:OptHavingClause
    // XXX NOTYET OptQualifyClause
    // XXX NOTYET OptWindowClause
    {
      sel := &ast.Select{
          Kind: "Select",
          Distinct: distinct.(bool),
          Value: value.(bool),
          Selection: selection.(ast.Selection),
          Loc: loc(c),
      }
      if from != nil {
        sel.From = from.(*ast.From)
      }
      if where != nil {
        sel.Where = where.(ast.Expr)
      }
      if group != nil {
        sel.GroupBy = sliceOf[ast.Expr](group)
      }
      if having != nil {
        sel.Having = having.(ast.Expr)
      }
      return sel, nil
    }

FromSelect
  = from:FromOp _ SELECT 
    distinct:OptDistinct
    value:OptSelectValue
    //XXX for whitespace in front of target need it only if it begins with 
    // an identifier, e.g., select(1); is ok
    _ selection:Selection
    where:OptWhereClause
    group:OptGroupClause
    having:OptHavingClause
    // XXX NOTYET OptQualifyClause
    // XXX NOTYET OptWindowClause
    {
      sel := &ast.Select{
          Kind: "Select",
          Distinct: distinct.(bool),
          Value: value.(bool),
          Selection: selection.(ast.Selection),
          From: from.(*ast.From),
          Loc: loc(c),
      }
      if where != nil {
        sel.Where = where.(ast.Expr)
      }
      if group != nil {
        sel.GroupBy = sliceOf[ast.Expr](group)
      }
      if having != nil {
        sel.Having = having.(ast.Expr)
      }
      return sel, nil
    }

Values
  = VALUES _ exprs:Exprs {
      return &ast.Values{
          Kind: "Values",
          Exprs: sliceOf[ast.Expr](exprs),
          Loc: loc(c),
      }, nil
    }

OptDistinct
  = _ ALL        { return false, nil }
  / _ DISTINCT   { return true, nil }
  / ""              { return false, nil }

OptSelectValue
  = _ AS _ VALUE  { return true, nil }
  / _ VALUE          { return true, nil } // SQL++ form XXX decide which
  / ""                  { return false, nil }

OptWithClause 
  = WithClause
  / ""          { return nil, nil }

WithClause 
  = WITH r:OptRecursive _ ctes:CteList __ {
        return &ast.With{
            Kind: "With",
            Recursive: r.(bool), 
            CTEs: sliceOf[ast.CTE](ctes),
            Loc: loc(c),
        }, nil
    }

OptRecursive 
  = _ RECURSIVE  { return true, nil }
  / ""              { return false, nil }

CteList = first:Cte rest:( __ "," __ cte:Cte { return cte, nil} )* {
    return prepend(first, rest), nil 
}

Cte
  = name:SQLIdentifier _ AS m:OptMaterialized __ "(" __ s:SQLPipe __ ")" {
        return ast.CTE{
            Name: name.(*ast.ID),
            Materialized: m.(bool),
            Body: s.(*ast.SQLPipe),
            Loc: loc(c),
        }, nil
    }

OptMaterialized 
  = _ MATERIALIZED _             { return true, nil }
  / _ NOT _ MATERIALIZED _    { return false, nil }
  / ""                              { return false, nil }

OptAllClause
  = _ ALL 
  / ""

OptFromClause
  = _ from:FromOp {
      return from, nil
    }
  / ""                  { return nil, nil }

OptWhereClause
  = where:WhereClause     { return where, nil }
  / ""                      { return nil, nil }

OptGroupClause
  = _ group:GroupClause { return group, nil }
  / ""                  { return nil, nil }

GroupClause
  = GROUP _ BY _ list:GroupByList { return list, nil }

GroupByList 
  = first:GroupByItem rest:( __ "," __ g:GroupByItem { return g, nil } )* {
      return prepend(first, rest), nil
    }

GroupByItem = Expr

OptHavingClause
  = _ h:HavingClause { return h, nil }
  / "" { return nil, nil }

HavingClause
  = HAVING _ e:Expr { return e, nil }

JoinOperation
  = CrossJoin
  / ConditionJoin

CrossJoin
  = left:FromElem CrossJoinOp right:FromElem {
        return &ast.CrossJoin{
            Kind: "CrossJoin",
            Left: left.(*ast.FromElem),
            Right: right.(*ast.FromElem),
            Loc: loc(c),
        }, nil
    }

CrossJoinOp 
  = __ "," __ 
  / _ CROSS _ JOIN _ 

ConditionJoin
  = left:FromElem style:SQLJoinStyle _ right:FromElem e:JoinExpr {
        return &ast.SQLJoin{
            Kind: "SQLJoin",
            Style: style.(string),
            Left: left.(*ast.FromElem),
            Right: right.(*ast.FromElem),
            Cond: e.(ast.JoinExpr),
            Loc: loc(c),
        }, nil
    }

SQLJoinStyle 
  = (_ INNER)? _ JOIN             { return "inner", nil }
  / _ FULL (_ OUTER)? _ JOIN   { return "full", nil }
  / _ LEFT (_ OUTER)? _ JOIN   { return "left", nil }
  / _ RIGHT (_ OUTER)? _ JOIN   { return "right", nil }

JoinExpr 
  = _ ON _ e:Expr { 
        return &ast.JoinOnExpr{
            Kind: "JoinOnExpr", 
            Expr: e.(ast.Expr),
            Loc: loc(c),
        }, nil
    }
  / _ USING __ "(" __ fields:Lvals __ ")" {
        return &ast.JoinUsingExpr{
            Kind: "JoinUsingExpr",
            Fields: sliceOf[ast.Expr](fields),
            Loc: loc(c),
        }, nil
    }

OptOrdinality 
  = _ WITH _ ORDINALITY   {
        return &ast.Ordinality{
            Kind: "Ordinality",
            Loc: loc(c),
        }, nil
    }
  / ""                          { return nil, nil }

OptAlias
  = _ a:AliasClause { return a, nil }
  / ""              { return nil, nil }

AliasClause
 = (AS _)? !(SQLGuard / DeprecatedFroms) name:IdentifierName cols:Columns? {
      alias := &ast.TableAlias{Kind: "TableAlias", Name: name.(string), Loc: loc(c)}
      if cols != nil {
        alias.Columns = cols.([]*ast.ID)
      }
      return alias, nil
    }

Columns
  = __ "(" __ first:SQLIdentifier rest:( __ "," __ s:SQLIdentifier{ return s, nil })* __ ")" {
        return sliceOf[*ast.ID](prepend(first,rest)), nil 
    }

Selection
  = first:SelectElem rest:( __ "," __ s:SelectElem{ return s, nil })* {
      return ast.Selection{
        Kind: "Selection",
        Args: sliceOf[ast.AsExpr](prepend(first, rest)),
        Loc: loc(c),
      }, nil
  }

//XXX need to add *, id.*, except, replace
SelectElem 
  = expr:(AggDistinct / Expr) as:OptAsClause {
      elem := ast.AsExpr{
        Kind: "AsExpr",
        Expr: expr.(ast.Expr),
        Loc: loc(c),
      }
      if as != nil {
          elem.Label = as.(*ast.ID)
      }
      return elem, nil
    }
  / "*" {
      // empty assignment is star (for now)
      return ast.AsExpr{Kind: "AsExpr"}, nil
    }

OptAsClause 
  = _ AS _ id:SQLIdentifier { return id, nil }
  / _ !SQLGuard id:SQLIdentifier { return id, nil }
  / ""  { return nil, nil }

OptOrderByClause 
  = _ ORDER _ BY _ list:OrderByList { 
        return &ast.OrderBy{
          Kind: "OrderBy",
          Exprs: list.([]ast.SortExpr),
          Loc: loc(c),
        }, nil
    }
  / ""          { return nil, nil }

OrderByList 
  = first:OrderByItem rest:( __ "," __ o:OrderByItem{ return o, nil})* {
      return sliceOf[ast.SortExpr](prepend(first, rest)), nil
    }

OrderByItem
  = e:Expr order:OptAscDesc nulls:OptNullsOrder {
      s := ast.SortExpr{Kind: "SortExpr", Expr: e.(ast.Expr), Loc: loc(c)}
      if order != nil {
        s.Order = order.(*ast.ID)
      }
      if nulls != nil {
        s.Nulls = nulls.(*ast.ID)
      }
      return s, nil
    }

OptAscDesc
  = _ ASC   { return &ast.ID{Kind: "ID", Name: "asc", Loc: loc(c)}, nil }
  / _ DESC { return &ast.ID{Kind: "ID", Name: "desc", Loc: loc(c)}, nil }
  / ""         { return nil, nil }

OptNullsOrder
  = _ NULLS _ FIRST   { return &ast.ID{Kind: "ID", Name: "first", Loc: loc(c)}, nil }
  / _ NULLS _ LAST    { return &ast.ID{Kind: "ID", Name: "last", Loc: loc(c)}, nil }
  / ""                      { return nil, nil }

OptSQLLimitOffset
  = _ op:SQLLimitOffset { return op, nil } 
  / ""                  { return nil, nil }

SQLLimitOffset
  = l:LimitClause o:OptOffsetClause {
      op := &ast.SQLLimitOffset{
        Kind: "SQLLimitOffset",
        Loc: loc(c),
      }
      if l != nil {
        op.Limit = l.(ast.Expr)
      }
      if o != nil {
        op.Offset = o.(ast.Expr)
      }
      return op, nil
    }
  / o:OffsetClause l:OptLimitClause {
      op := &ast.SQLLimitOffset{
        Kind: "SQLLimitOffset",
        Offset: o.(ast.Expr),
        Loc: loc(c),
      }
      if l != nil {
        op.Limit = l.(ast.Expr)
      }
      return op, nil
    }

OptLimitClause 
  = _ l:LimitClause { return l, nil }
  / ""              { return nil, nil }

LimitClause 
  = LIMIT _ ALL { return nil, nil }
  / LIMIT _ e:Expr { return e, nil }

OptOffsetClause 
  = _ o:OffsetClause  { return o, nil }
  / ""  { return nil, nil }

OffsetClause 
  = OFFSET _ e:Expr  { return e, nil }

SetOperation
  = left:SelectExpr distinct:SetOp _ right:SelectExpr {
        return &ast.Union{
            Kind: "Union",
            Distinct: distinct.(bool),
            Left: left.(ast.Op),
            Right: right.(ast.Op),
            Loc: loc(c),
        }, nil
    }

SetOp 
  = _ UNION _ ALL       { return false, nil }
  / _ UNION (_ DISTINCT)?  { return true, nil }

// XXX incomplete
SQLGuard =
    FROM / GROUP / HAVING / SELECT / RECURSIVE
  / INNER / LEFT / RIGHT / OUTER / CROSS / JOIN
  / UNION
  / ORDER
  / OFFSET
  / LIMIT
  / WHERE 
  / WITH
  / USING
  / ON

AGGREGATE  = "AGGREGATE"i       !IdentifierRest
ALL        = "ALL"i             !IdentifierRest
AND        = "AND"i             !IdentifierRest { return "and", nil }
ANTI       = "ANTI"i            !IdentifierRest
AS         = "AS"i              !IdentifierRest
ASC        = "ASC"i             !IdentifierRest { return "asc", nil }
ASSERT     = "ASSERT"i          !IdentifierRest
AT         = "AT"i              !IdentifierRest
AUTHOR     = "AUTHOR"i          !IdentifierRest
BETWEEN    = "BETWEEN"i         !IdentifierRest
BODY       = "BODY"i            !IdentifierRest
BY         = "BY"i              !IdentifierRest
CASE       = "CASE"i            !IdentifierRest
CAST       = "CAST"i            !IdentifierRest
CONST      = "CONST"i           !IdentifierRest
COUNT      = "COUNT"i           !IdentifierRest
CROSS      = "CROSS"i           !IdentifierRest
CUT        = "CUT"i             !IdentifierRest
DATE       = "DATE"i            !IdentifierRest { return "date", nil }
DEBUG      = "DEBUG"i           !IdentifierRest
DEFAULT    = "DEFAULT"i         !IdentifierRest
DESC       = "DESC"i            !IdentifierRest { return "desc", nil }
DISTINCT   = "DISTINCT"i        !IdentifierRest
DROP       = "DROP"i            !IdentifierRest 
ELSE       = "ELSE"i            !IdentifierRest
END        = "END"i             !IdentifierRest
ERROR      = "ERROR"i           !IdentifierRest
EVAL       = "EVAL"i            !IdentifierRest
EXPLODE    = "EXPLODE"i         !IdentifierRest
EXTRACT    = "EXTRACT"i         !IdentifierRest
FALSE      = "FALSE"i           !IdentifierRest
FIRST      = "FIRST"i           !IdentifierRest
FOR        = "FOR"i             !IdentifierRest
FORK       = "FORK"i            !IdentifierRest
FORMAT     = "FORMAT"i          !IdentifierRest
FROM       = "FROM"i            !IdentifierRest
FULL       = "FULL"i            !IdentifierRest
FUNC       = "FUNC"i            !IdentifierRest
FUSE       = "FUSE"i            !IdentifierRest
GREP       = "GREP"i            !IdentifierRest
GROUP      = "GROUP"i           !IdentifierRest
HAVING     = "HAVING"i          !IdentifierRest
HEAD       = "HEAD"i            !IdentifierRest 
HEADERS    = "HEADERS"i         !IdentifierRest
IN         = "IN"i              !IdentifierRest
INNER      = "INNER"i           !IdentifierRest
IS         = "IS"i              !IdentifierRest
JOIN       = "JOIN"i            !IdentifierRest
LAST       = "LAST"i            !IdentifierRest
LEFT       = "LEFT"i            !IdentifierRest
LIKE       = "LIKE"i           !IdentifierRest
LIMIT      = "LIMIT"i           !IdentifierRest
LOAD       = "LOAD"i            !IdentifierRest
MATERIALIZED = "MATERIALIZED"i  !IdentifierRest
MERGE      = "MERGE"i           !IdentifierRest
MESSAGE    = "MESSAGE"i         !IdentifierRest
META       = "META"i            !IdentifierRest
METHOD     = "METHOD"i          !IdentifierRest
NOT        = "NOT"i             !IdentifierRest
NULL       = "NULL"i            !IdentifierRest
NULLS      = "NULLS"i           !IdentifierRest
OFFSET     = "OFFSET"i          !IdentifierRest
ON         = "ON"i              !IdentifierRest
OP         = "OP"i              !IdentifierRest
OR         = "OR"i              !IdentifierRest { return "or", nil }
ORDER      = "ORDER"i           !IdentifierRest
ORDINALITY = "ORDINALITY"i      !IdentifierRest
OUTER      = "OUTER"i           !IdentifierRest
OUTPUT     = "OUTPUT"i          !IdentifierRest
OVER       = "OVER"i            !IdentifierRest
PASS       = "PASS"i            !IdentifierRest
PUT        = "PUT"i             !IdentifierRest
RECURSIVE  = "RECURSIVE"        !IdentifierRest
REGEXP     = "REGEXP"i          !IdentifierRest
REGEXP_REPLACE = "REGEXP_REPLACE"i !IdentifierRest
RENAME     = "RENAME"i          !IdentifierRest
RIGHT      = "RIGHT"i           !IdentifierRest
SAMPLE     = "SAMPLE"i          !IdentifierRest
SEARCH     = "SEARCH"i          !IdentifierRest
SELECT     = "SELECT"i          !IdentifierRest
SHAPE      = "SHAPE"i           !IdentifierRest
SKIP       = "SKIP"i            !IdentifierRest
SORT       = "SORT"i            !IdentifierRest
SUBSTRING  = "SUBSTRING"i       !IdentifierRest
SUMMARIZE  = "SUMMARIZE"i       !IdentifierRest
SWITCH     = "SWITCH"i          !IdentifierRest
TAIL       = "TAIL"i            !IdentifierRest 
TAP        = "TAP"i             !IdentifierRest
THEN       = "THEN"i            !IdentifierRest
TIMESTAMP  = "TIMESTAMP"i       !IdentifierRest { return "timestamp", nil }
TOP        = "TOP"i             !IdentifierRest
TRUE       = "TRUE"i            !IdentifierRest
TYPE       = "TYPE"i            !IdentifierRest
UNION      = "UNION"i           !IdentifierRest
UNIQ       = "UNIQ"i            !IdentifierRest 
UNNEST     = "UNNEST"i          !IdentifierRest
USING      = "USING"i           !IdentifierRest
VALUE      = "VALUE"i           !IdentifierRest
VALUES     = "VALUES"i          !IdentifierRest
WHEN       = "WHEN"i            !IdentifierRest
WHERE      = "WHERE"i           !IdentifierRest
WITH       = "WITH"i            !IdentifierRest
YIELD      = "YIELD"i           !IdentifierRest
